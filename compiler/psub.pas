{
    Copyright (c) 1998-2002 by Florian Klaempfl, Daniel Mantione

    Does the parsing and codegeneration at subroutine level

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

 ****************************************************************************
}
unit psub;

{$i fpcdefs.inc}

{ $define debug_eh}

interface

    uses
      globals,cclasses,
      node,nbas,nutils,aasmdata,
      symdef,procinfo,optdfa;

    type
      tcgprocinfo = class(tprocinfo)
      private type
        ttempinfo_flags_entry = record
          tempinfo : ptempinfo;
          flags : ttempinfoflags;
        end;
        ptempinfo_flags_entry = ^ttempinfo_flags_entry;
      private
        tempinfo_flags_map : TFPList;
        tempflags_swapped : boolean;
        procedure swap_tempflags;
        function store_node_tempflags(var n: tnode; arg: pointer): foreachnoderesult;
        procedure CreateInlineInfo;
        { returns the node which is the start of the user code, this is needed by the dfa }
        function GetUserCode: tnode;
        procedure maybe_add_constructor_wrapper(var tocode: tnode; withexceptblock: boolean);
        procedure add_entry_exit_code;
        procedure setup_tempgen;
        procedure TransformNodeTree;
        procedure convert_captured_syms;
      protected
        procedure generate_code_exceptfilters;
      public
        { code for the subroutine as tree }
        code : tnode;
        { positions in the tree for init/final }
        entry_asmnode,
        loadpara_asmnode,
        exitlabel_asmnode,
        stackcheck_asmnode,
        init_asmnode    : tasmnode;
        temps_finalized : boolean;
        dfabuilder : TDFABuilder;
        { Last temp offset of the parent procedure at the time exceptfilter
          code generation begins. Temps below this boundary belong to the parent
          and must be adjusted to FP-relative in the handler. Temps at or above
          this boundary belong to the handler and stay SP-relative.
          Only used on AArch64-Win64. }
        exceptfilter_parent_tempend : longint;

        destructor  destroy;override;

        function calc_stackframe_size : longint;override;

        procedure printproc(pass:string);
        procedure generate_code;
        procedure generate_code_tree;
        procedure generate_exceptfilter(nestedpi: tcgprocinfo);
        procedure generate_exit_label(list: tasmlist); virtual;
        procedure resetprocdef;
        procedure add_to_symtablestack;
        procedure remove_from_symtablestack;
        procedure parse_body;
        procedure set_code(p: tnode); override;

        procedure store_tempflags;
        procedure apply_tempflags;
        procedure reset_tempflags;

        function has_assembler_child : boolean;
        procedure set_eh_info; override;
{$ifdef DEBUG_NODE_XML}
        procedure XMLPrintProc(FirstHalf: Boolean);
{$endif DEBUG_NODE_XML}
      end;

      tread_proc_flag = (
        rpf_classmethod,
        rpf_generic,
        rpf_anonymous
      );
      tread_proc_flags = set of tread_proc_flag;


    procedure printnode_reset;

{$ifdef DEBUG_NODE_XML}
    procedure XMLInitializeNodeFile(RootName, ModuleName: shortstring);
    procedure XMLFinalizeNodeFile(RootName: shortstring);
{$endif DEBUG_NODE_XML}
    { reads the declaration blocks }
    procedure read_declarations(islibrary : boolean);

    { reads declarations in the interface part of a unit }
    procedure read_interface_declarations;

    { reads any routine in the implementation, or a non-method routine
      declaration in the interface (depending on whether or not parse_only is
      true) }
    function read_proc(flags:tread_proc_flags; usefwpd: tprocdef):tprocdef;

    { parses `begin..end` as a parameterless anonymous procedure (for the
      `async begin..end` block form) and returns its procdef; positioned at the
      `begin` token }
    function read_async_block:tprocdef;

    { parses only the body of a non nested routine; needs a correctly setup pd }
    procedure read_proc_body(pd:tprocdef);

    { -OoIPACP: scan the already-parsed main program body (MAINPI, potype_proginit)
      for call sites that pass compile-time constants to eligible parameters of
      stashed routines, retarget them to specialized clones, and compile those
      clones.  Must be called after MAINPI.parse_body and before its
      generate_code_tree, with the module static symtable on the symtablestack. }
    procedure ipacp_process_main_body(mainpi:tcgprocinfo);

    procedure import_external_proc(pd:tprocdef);


implementation

    uses
       sysutils,
       { common }
       cutils, cmsgs, cdynset,
       { global }
       globtype,tokens,verbose,comphook,constexp,
       systems,cpubase,aasmbase,aasmtai,
       { symtable }
       symconst,symbase,symsym,symtype,symtable,defutil,defcmp,procdefutil,symcreat,
       paramgr,
       fmodule,
       { pass 1 }
       ngenutil,nld,ncal,ncon,nflw,nadd,ncnv,nmem,ninl,compinnr,htypechk,
       pass_1,
    {$ifdef state_tracking}
       nstate,
    {$endif state_tracking}
       { pass 2 }
{$ifndef NOPASS2}
       pass_2,
{$endif}
       { parser }
       scanner,gendef,
       pbase,pstatmnt,pdecl,pdecsub,pexports,pgenutil,pparautl,
       { codegen }
       tgobj,cgbase,cgobj,hlcgobj,hlcgcpu,dbgbase,

       ncgflw,
       ncgutil,

       optbase,
       opttree,
       opttail,
       optcse,
       optloop,
       optfinalvalue,
       optdevirt,
       optpure,
       optpartialinline,
       optipacp,
       optipara,
       opticf,
       optsra,
       optconstprop,
       optdeadstore,
       optloadmodifystore,
       optcall,
       optutils
{$if defined(arm) or defined(m68k)}
       ,cpuinfo
{$endif defined(arm) or defined(m68k)}
       {$ifndef NOOPT}
       ,aopt
       {$endif}
       ;

    { FPC Unleashed helpers: decide whether the asm STATEMENT blocks in an
      inline routine's body can be spliced into a caller.  An asm block is
      "inline-safe" when none of its operands reference a local variable,
      parameter or the function result -- those show up as top_local operands
      (resolved through tabstractnormalvarsym.localloc at codegen) or, for the
      TP-style INLINE() form, as ait_const entries whose symbol is still an
      unresolved AB_NONE local placeholder.  Only registers, immediates and
      global symbols survive verbatim relocation into another frame. }
    { returns '' when the block is inline-safe, otherwise the reason it is not }
    function inline_asm_block_reason(p_asm: TAsmList): string;
      var
        hp : tai;
        i  : longint;
      begin
        result:='';
        if not assigned(p_asm) then
          exit;
        hp:=tai(p_asm.first);
        while assigned(hp) do
          begin
            case hp.typ of
              ait_instruction :
                for i:=0 to tai_cpu_abstract(hp).ops-1 do
                  if tai_cpu_abstract(hp).oper[i]^.typ=top_local then
                    exit('assembler block referencing a local variable, parameter or function result');
              ait_const :
                if assigned(tai_const(hp).sym) and
                   (tai_const(hp).sym.bind=AB_NONE) then
                  exit('assembler block referencing a local variable, parameter or function result');
              ait_label :
                { local asm labels are not yet uniqued per inline site }
                if assigned(tai_label(hp).labsym) and (tai_label(hp).labsym.bind=AB_LOCAL) then
                  exit('assembler block defining a label');
              else
                ;
            end;
            hp:=tai(hp.next);
          end;
      end;

    function inline_asm_uses_local(var n: tnode; arg: pointer): foreachnoderesult;
      var
        reason : string;
      begin
        result:=fen_false;
        if (n.nodetype=asmn) and
           not(asmnf_get_asm_position in tasmnode(n).asmnodeflags) then
          begin
            reason:=inline_asm_block_reason(tasmnode(n).p_asm);
            if reason<>'' then
              begin
                pshortstring(arg)^:=reason;
                result:=fen_norecurse_true;
              end;
          end;
      end;


    function checknodeinlining(procdef: tprocdef): boolean;

      procedure _no_inline(const reason: TMsgStr);
        begin
          include(procdef.implprocoptions,pio_inline_not_possible);
          { stash the reason so the call-site cg_n_no_inline note can report it
            (for same-unit callers); see tprocdef.inlinenoreason }
          procdef.inlinenoreason:=reason;
          Message1(parser_n_not_supported_for_inline,reason);
          Message(parser_h_inlining_disabled);
        end;

      var
        i : integer;
        currpara : tparavarsym;
        asmreason : shortstring;
      begin
        result := false;
        { this code will never be used (only specialisations can be inlined),
          and moreover contains references to defs that are not stored in the
          ppu file }
        if df_generic in current_procinfo.procdef.defoptions then
          exit;
        { A pure `assembler;` routine gets its parameters, result and (for
          get_pc_addr-style helpers) return address through the ABI calling
          convention -- the asm body reads bare ABI registers / the return slot,
          none of which exist once the routine is spliced without a call. Such
          routines must NEVER be node-inlined, so keep the historical refusal. }
        if pi_is_assembler in current_procinfo.flags then
          begin
            if pi_has_assembler_block in current_procinfo.flags then
              begin
                _no_inline('assembler');
                exit;
              end;
          end
        else if pi_has_assembler_block in current_procinfo.flags then
          begin
            { FPC Unleashed: inner `asm ... end` STATEMENT blocks used to block
              inlining unconditionally. We now allow inlining as long as no asm
              operand references a local variable, parameter or the function
              result (top_local operands / TP-style INLINE ait_const refs):
              such a block only touches registers, immediates and global symbols,
              so it can be spliced verbatim into the caller (labels are made
              unique at the inline site via asmnf_inline_copy). Operands that
              reference locals/params would need those locals to be materialised
              in the caller's frame before tcgasmnode.ResolveRef can bind them,
              which is not yet implemented -- refuse those with a precise reason. }
            asmreason:='';
            if assigned(tcgprocinfo(current_procinfo).code) then
              foreachnodestatic(tcgprocinfo(current_procinfo).code,
                @inline_asm_uses_local,@asmreason);
            if asmreason<>'' then
              begin
                _no_inline(asmreason);
                exit;
              end;
          end;
        if (pi_has_global_goto in current_procinfo.flags) or
           (pi_has_interproclabel in current_procinfo.flags) then
          begin
            _no_inline('global goto');
            exit;
          end;
        if pi_has_nested_exit in current_procinfo.flags then
          begin
            _no_inline('nested exit');
            exit;
          end;
        if pi_calls_c_varargs in current_procinfo.flags then
          begin
            _no_inline('called C-style varargs functions');
            exit;
          end;
        { the compiler cannot handle inherited in inlined subroutines because
          it tries to search for self in the symtable, however, the symtable
          is not available }
        if pi_has_inherited in current_procinfo.flags then
          begin
            _no_inline('inherited');
            exit;
          end;
        if pio_nested_access in procdef.implprocoptions then
         begin
           _no_inline('access to local from nested scope');
           exit;
         end;
        { We can't support inlining for procedures that have nested
          procedures because the nested procedures use a fixed offset
          for accessing locals in the parent procedure (PFV) }
        if current_procinfo.has_nestedprocs then
          begin
            _no_inline('nested procedures');
            exit;
          end;

        if pi_uses_get_frame in current_procinfo.flags then
          begin
            _no_inline('get_frame');
            { for LLVM: it can inline things that FPC can't, but it mustn't
              inline this one }
            include(current_procinfo.procdef.implprocoptions,pio_inline_forbidden);
            exit;
          end;

        for i:=0 to procdef.paras.count-1 do
          begin
            currpara:=tparavarsym(procdef.paras[i]);
            case currpara.vardef.typ of
              arraydef :
                begin
                  if is_array_of_const(currpara.vardef) or
                     is_variant_array(currpara.vardef) then
                    begin
                      _no_inline('array of const');
                      exit;
                    end;
                  { open arrays might need re-basing of the index, i.e. if you pass
                    an array[1..10] as open array, you have to add 1 to all index operations
                    if you directly inline it }
                  if is_open_array(currpara.vardef) then
                    begin
                      _no_inline('open array');
                      exit;
                    end;
                end;
              else
                ;
            end;
        end;
        result:=true;
      end;


{****************************************************************************
                      PROCEDURE/FUNCTION BODY PARSING
****************************************************************************}

    procedure initializevars(p:TObject;arg:pointer);
      var
        b : tblocknode;
      begin
        if not (tsym(p).typ in [localvarsym,staticvarsym]) then
         exit;
        with tabstractnormalvarsym(p) do
         begin
           if assigned(defaultconstsym) then
            begin
              b:=tblocknode(arg);
              b.left:=cstatementnode.create(
                        cassignmentnode.create(
                            cloadnode.create(tsym(p),tsym(p).owner),
                            cloadnode.create(defaultconstsym,defaultconstsym.owner)),
                        b.left);
            end
         end;
      end;


    procedure check_finalize_paras(p:TObject;arg:pointer);
      begin
        if (tsym(p).typ=paravarsym) then
          begin
            if tparavarsym(p).needs_finalization then
              begin
                include(current_procinfo.flags,pi_needs_implicit_finally);
                include(current_procinfo.flags,pi_do_call);
              end;
          end;
      end;


    procedure check_finalize_locals(p:TObject;arg:pointer);
      begin
        { include the result: it needs to be finalized in case an exception }
        { occurs                                                            }
        if (tsym(p).typ=localvarsym) and
           (tlocalvarsym(p).refs>0) and
           is_managed_type(tlocalvarsym(p).vardef) then
          begin
            include(current_procinfo.flags,pi_needs_implicit_finally);
            include(current_procinfo.flags,pi_do_call);
          end;
      end;

    procedure init_main_block_syms(block: tnode);
      var
         oldfilepos: tfileposinfo;
         blk_i: longint;
      begin
        { splice the guarded per-thread initializers from `threadstatic`
          declaration sections to the front of the body; done before the
          local-var defaults below so those still run first, matching the
          inline form where the section sits after the var section }
        if assigned(current_procinfo.threadstatic_initcode) then
         begin
           if assigned(block) and (block.nodetype=blockn) then
             begin
               tblocknode(block).left:=cstatementnode.create(
                 current_procinfo.threadstatic_initcode,tblocknode(block).left);
               current_procinfo.threadstatic_initcode:=nil;
             end;
         end;

        { initialized variables }
        if current_procinfo.procdef.localst.symtabletype=localsymtable then
         begin
           { initialization of local variables with their initial
             values: part of function entry }
           oldfilepos:=current_filepos;
           current_filepos:=current_procinfo.entrypos;
           current_procinfo.procdef.localst.SymList.ForEachCall(@initializevars,block);
           if assigned(current_procinfo.procdef.blocklocalsymtables) then
             for blk_i:=0 to current_procinfo.procdef.blocklocalsymtables.count-1 do
               TSymtable(current_procinfo.procdef.blocklocalsymtables[blk_i]).SymList.ForEachCall(@initializevars,block);
           current_filepos:=oldfilepos;
         end;

        if assigned(current_procinfo.procdef.parentfpstruct) then
         begin
           { finish the parentfpstruct (add padding, ...) }
           finish_parentfpstruct(current_procinfo.procdef);
         end;
      end;

    function block(islibrary : boolean) : tnode;
      begin
         { parse const,types and vars }
         read_declarations(islibrary);

         { do we have an assembler block without the po_assembler?
           we should allow this for Delphi compatibility (PFV) }
         if (current_scanner.token=_ASM) and (m_delphi in current_settings.modeswitches) then
           include(current_procinfo.procdef.procoptions,po_assembler);

         { Handle assembler block different }
         if (po_assembler in current_procinfo.procdef.procoptions) then
          begin
            block:=assembler_block;
            exit;
          end;

         {Unit initialization?.}
         if (
             assigned(current_procinfo.procdef.localst) and
             (current_procinfo.procdef.localst.symtablelevel=main_program_level) and
             (current_module.is_unit or islibrary)
            ) then
           begin
             if (current_scanner.token=_END) then
                begin
                   consume(_END);
                   { We need at least a node, else the entry/exit code is not
                     generated and thus no PASCALMAIN symbol which we need (PFV) }
                   if islibrary then
                    block:=cnothingnode.create
                   else
                    block:=nil;
                end
              else
                begin
                   if current_scanner.token=_INITIALIZATION then
                     begin
                        { The library init code is already called and does not
                          need to be in the initfinal table (PFV) }
                        block:=statement_block(_INITIALIZATION);
                        init_main_block_syms(block);
                     end
                   else if current_scanner.token=_FINALIZATION then
                     begin
                       { when a unit has only a finalization section, we can come to this
                         point when we try to read the nonh existing initialization section
                         so we've to check if we are really try to parse the finalization }
                       if current_procinfo.procdef.proctypeoption=potype_unitfinalize then
                         block:=statement_block(_FINALIZATION)
                       else
                         block:=nil;
                     end
                   else
                     block:=statement_block(_BEGIN);
                end;
            end
         else
            begin
               { parse routine body }
               current_procinfo.parsing_main_block:=true;
               block:=statement_block(_BEGIN);
               init_main_block_syms(block);
            end;
      end;


{****************************************************************************
                       PROCEDURE/FUNCTION COMPILING
****************************************************************************}

    procedure printnode_reset;
      begin
        assign(printnodefile,treelogfilename);
        {$push}{$I-}
         rewrite(printnodefile);
        {$pop}
        if ioresult<>0 then
         begin
           Comment(V_Error,'Error creating '+treelogfilename);
           exit;
         end;
        close(printnodefile);
      end;


    procedure add_label_init(p:TObject;arg:pointer);
      begin
        if tstoredsym(p).typ=labelsym then
          begin
            addstatement(tstatementnode(arg^),
              cifnode.create(caddnode.create(equaln,
                ccallnode.createintern('fpc_setjmp',
                  ccallparanode.create(cloadnode.create(tlabelsym(p).jumpbuf,tlabelsym(p).jumpbuf.owner),nil)),
                cordconstnode.create(1,search_system_proc('fpc_setjmp').returndef,true))
              ,cgotonode.create(tlabelsym(p)),nil)
            );
          end;
      end;


    { seed auto-property backing fields with their initializer values; runs at
      constructor entry right after allocation, so the user body can override }
    procedure add_auto_property_inits(structdef: tabstractrecorddef; var newstatement: tstatementnode);
      var
        od      : tobjectdef;
        i       : longint;
        fieldvs : tfieldvarsym;
      begin
        if structdef.typ<>objectdef then
          exit;
        od:=tobjectdef(structdef);
        if not assigned(od.auto_prop_init_fields) then
          exit;
        for i:=0 to od.auto_prop_init_fields.count-1 do
          begin
            fieldvs:=tfieldvarsym(od.auto_prop_init_fields[i]);
            addstatement(newstatement,
              cassignmentnode.create(
                csubscriptnode.create(fieldvs,load_self_node),
                tnode(od.auto_prop_init_values[i]).getcopy));
          end;
      end;


    function generate_bodyentry_block:tnode;
      var
        srsym        : tsym;
        para         : tcallparanode;
        call         : tcallnode;
        newstatement : tstatementnode;
        def          : tabstractrecorddef;
      begin
        result:=internalstatements(newstatement);

        if assigned(current_structdef) then
          begin
            { a constructor needs a help procedure }
            if (current_procinfo.procdef.proctypeoption=potype_constructor) then
              begin
                if is_class(current_structdef) or
                    (
                      is_objectpascal_helper(current_structdef) and
                      is_class(tobjectdef(current_structdef).extendeddef)
                    ) then
                  begin
                    if is_objectpascal_helper(current_structdef) then
                      def:=tabstractrecorddef(tobjectdef(current_structdef).extendeddef)
                    else
                      def:=current_structdef;
                    srsym:=search_struct_member(def,'NEWINSTANCE');
                    if assigned(srsym) and
                       (srsym.typ=procsym) then
                      begin
                        { if vmt=1 then newinstance }
                        call:=
                          ccallnode.create(nil,tprocsym(srsym),srsym.owner,
                            ctypeconvnode.create_internal(load_self_pointer_node,cclassrefdef.create(current_structdef)),
                            [],nil);
                        include(call.callnodeflags,cnf_ignore_devirt_wpo);
                        addstatement(newstatement,cifnode.create(
                            caddnode.create_internal(equaln,
                                ctypeconvnode.create_internal(
                                    load_vmt_pointer_node,
                                    voidpointertype),
                                cpointerconstnode.create(1,voidpointertype)),
                            cassignmentnode.create(
                                ctypeconvnode.create_internal(
                                    load_self_pointer_node,
                                    voidpointertype),
                                call),
                            nil));
                      end
                    else
                      Message(parser_e_no_suitable_newinstance_method_found);
                  end
                else
                  if is_object(current_structdef) then
                    begin
                      { parameter 3 : vmt_offset }
                      { parameter 2 : address of pointer to vmt,
                        this is required to allow setting the vmt to -1 to indicate
                        that memory was allocated }
                      { parameter 1 : self pointer }
                      para:=ccallparanode.create(
                                cordconstnode.create(tobjectdef(current_structdef).vmt_offset,s32inttype,false),
                            ccallparanode.create(
                                ctypeconvnode.create_internal(
                                    load_vmt_pointer_node,
                                    voidpointertype),
                            ccallparanode.create(
                                ctypeconvnode.create_internal(
                                    load_self_pointer_node,
                                    voidpointertype),
                            nil)));
                      addstatement(newstatement,cassignmentnode.create(
                          ctypeconvnode.create_internal(
                              load_self_pointer_node,
                              voidpointertype),
                          ccallnode.createintern('fpc_help_constructor',para)));
                    end
                else
                  if is_javaclass(current_structdef) or
                     ((target_info.system in systems_jvm) and
                      is_record(current_structdef)) then
                    begin
                      if (current_procinfo.procdef.proctypeoption=potype_constructor) and
                         not current_procinfo.ConstructorCallingConstructor then
                       begin
                         { call inherited constructor }
                         if is_javaclass(current_structdef) then
                           srsym:=search_struct_member_no_helper(tobjectdef(current_structdef).childof,'CREATE')
                         else
                           srsym:=search_struct_member_no_helper(java_fpcbaserecordtype,'CREATE');
                         if assigned(srsym) and
                            (srsym.typ=procsym) then
                           begin
                             call:=ccallnode.create(nil,tprocsym(srsym),srsym.owner,load_self_node,[cnf_inherited],nil);
                             exclude(tcallnode(call).callnodeflags,cnf_return_value_used);
                             addstatement(newstatement,call);
                           end
                         else
                           internalerror(2011010312);
                       end;
                    end
                else
                  if not is_record(current_structdef) and
                     not (
                            is_objectpascal_helper(current_structdef) and
                            (tobjectdef(current_structdef).extendeddef.typ<>objectdef)
                         ) then
                    internalerror(200305103);
                { if self=nil then exit
                  calling fail instead of exit is useless because
                  there is nothing to dispose (PFV) }
                if is_class_or_object(current_structdef) then
                  addstatement(newstatement,cifnode.create(
                    caddnode.create(equaln,
                        load_self_pointer_node,
                        cnilnode.create),
                    cexitnode.create(nil),
                    nil));
                { seed auto-property backing fields before the user body runs }
                add_auto_property_inits(current_structdef,newstatement);
              end;

            { maybe call BeforeDestruction for classes }
            if (current_procinfo.procdef.proctypeoption=potype_destructor) and
               is_class(current_structdef) then
              begin
                srsym:=search_struct_member(current_structdef,'BEFOREDESTRUCTION');
                if assigned(srsym) and
                   (srsym.typ=procsym) then
                  begin
                    { if vmt>0 then beforedestruction }
                    addstatement(newstatement,cifnode.create(
                        caddnode.create(gtn,
                            ctypeconvnode.create_internal(
                              load_vmt_pointer_node,ptrsinttype),
                            ctypeconvnode.create_internal(
                              cnilnode.create,ptrsinttype)),
                        ccallnode.create(nil,tprocsym(srsym),srsym.owner,load_self_node,[],nil),
                        nil));
                  end
                else
                  internalerror(200305104);
              end;
          end;
        if m_non_local_goto in current_settings.modeswitches then
          tsymtable(current_procinfo.procdef.localst).SymList.ForEachCall(@add_label_init,@newstatement);

        initialize_capturer(current_procinfo,newstatement);
      end;


    function generate_bodyexit_block:tnode;
      var
        srsym : tsym;
        para : tcallparanode;
        newstatement : tstatementnode;
        oldlocalswitches: tlocalswitches;
      begin
        result:=internalstatements(newstatement);

        if assigned(current_structdef) then
          begin
            { Don't test self and the vmt here. The reason is that  }
            { a constructor already checks whether these are valid  }
            { before. Further, in case of TThread the thread may    }
            { free the class instance right after AfterConstruction }
            { has been called, so it may no longer be valid (JM)    }
            oldlocalswitches:=current_settings.localswitches;
            current_settings.localswitches:=oldlocalswitches-[cs_check_object,cs_check_range];

            { a destructor needs a help procedure }
            if (current_procinfo.procdef.proctypeoption=potype_destructor) then
              begin
                if is_class(current_structdef) then
                  begin
                    srsym:=search_struct_member(current_structdef,'FREEINSTANCE');
                    if assigned(srsym) and
                       (srsym.typ=procsym) then
                      begin
                        { if self<>0 and vmt<>0 then freeinstance }
                        addstatement(newstatement,cifnode.create(
                            caddnode.create(andn,
                                caddnode.create(unequaln,
                                    load_self_pointer_node,
                                    cnilnode.create),
                                caddnode.create(unequaln,
                                    ctypeconvnode.create(
                                        load_vmt_pointer_node,
                                        voidpointertype),
                                    cpointerconstnode.create(0,voidpointertype))),
                            ccallnode.create(nil,tprocsym(srsym),srsym.owner,load_self_node,[],nil),
                            nil));
                      end
                    else
                      internalerror(2003051001);
                  end
                else
                  if is_object(current_structdef) then
                    begin
                      { finalize object data, but only if not in inherited call }
                      if is_managed_type(current_structdef) then
                        begin
                          addstatement(newstatement,cifnode.create(
                            caddnode.create(unequaln,
                              ctypeconvnode.create_internal(load_vmt_pointer_node,voidpointertype),
                              cnilnode.create),
                            cnodeutils.finalize_data_node(load_self_node),
                            nil));
                        end;
                      { parameter 3 : vmt_offset }
                      { parameter 2 : pointer to vmt }
                      { parameter 1 : self pointer }
                      para:=ccallparanode.create(
                                cordconstnode.create(tobjectdef(current_structdef).vmt_offset,s32inttype,false),
                            ccallparanode.create(
                                ctypeconvnode.create_internal(
                                    load_vmt_pointer_node,
                                    voidpointertype),
                            ccallparanode.create(
                                ctypeconvnode.create_internal(
                                    load_self_pointer_node,
                                    voidpointertype),
                            nil)));
                      addstatement(newstatement,
                          ccallnode.createintern('fpc_help_destructor',para));
                    end
                else if is_javaclass(current_structdef) then
                  begin
                    { nothing to do }
                  end
                else
                  internalerror(200305105);
              end;
            current_settings.localswitches:=oldlocalswitches;
          end;
      end;


{****************************************************************************
                                  TCGProcInfo
****************************************************************************}

     destructor tcgprocinfo.destroy;
       begin
         TFPList.FreeAndNilDisposing(tempinfo_flags_map,TypeInfo(ttempinfo_flags_entry));
         code.free;
         code := nil;
         inherited destroy;
       end;


    function tcgprocinfo.calc_stackframe_size:longint;
      begin
        result:=Align(tg.direction*tg.lasttemp,current_settings.alignment.localalignmin);
      end;


    procedure tcgprocinfo.printproc(pass:string);
      begin
        assign(printnodefile,treelogfilename);
        {$push}{$I-}
         append(printnodefile);
         if ioresult<>0 then
          rewrite(printnodefile);
        {$pop}
        if ioresult<>0 then
         begin
           Comment(V_Error,'Error creating '+treelogfilename);
           exit;
         end;
        writeln(printnodefile);
        writeln(printnodefile,'*******************************************************************************');
        writeln(printnodefile, pass);
        writeln(printnodefile,procdef.fullprocname(false));
        writeln(printnodefile,'*******************************************************************************');
        printnode(printnodefile,code);
        close(printnodefile);
      end;


    procedure tcgprocinfo.maybe_add_constructor_wrapper(var tocode: tnode; withexceptblock: boolean);
      var
        oldlocalswitches: tlocalswitches;
        srsym: tsym;
        constructionblock,
        exceptblock,
        newblock: tblocknode;
        newstatement: tstatementnode;
        pd: tprocdef;
        constructionsuccessful: tlocalvarsym;
      begin
        if assigned(procdef.struct) and
           (procdef.proctypeoption=potype_constructor) then
          begin
            withexceptblock:=
              withexceptblock and
              not(target_info.system in systems_garbage_collected_managed_types);
            { Don't test self and the vmt here. See generate_bodyexit_block }
            { why (JM)                                                      }
            oldlocalswitches:=current_settings.localswitches;
            current_settings.localswitches:=oldlocalswitches-[cs_check_object,cs_check_range];

            { call AfterConstruction for classes }
            constructionsuccessful:=nil;
            if is_class(procdef.struct) then
              begin
                constructionsuccessful:=clocalvarsym.create(internaltypeprefixName[itp_vmt_afterconstruction_local],vs_value,ptrsinttype,[]);
                procdef.localst.insertsym(constructionsuccessful,false);
                srsym:=search_struct_member(procdef.struct,'AFTERCONSTRUCTION');
                if not assigned(srsym) or
                   (srsym.typ<>procsym) then
                  internalerror(200305106);

                current_filepos:=entrypos;
                constructionblock:=internalstatements(newstatement);
                { initialise constructionsuccessful with -1, indicating that
                  the construction was not successful and hence
                  beforedestruction should not be called if a destructor is
                  called from the constructor }
                addstatement(newstatement,cassignmentnode.create(
                  cloadnode.create(constructionsuccessful,procdef.localst),
                  genintconstnode(-1))
                );
                { first execute all constructor code. If no exception
                  occurred then we will execute afterconstruction,
                  otherwise we won't (the exception will jump over us) }
                addstatement(newstatement,tocode);
                current_filepos:=exitpos;
                { if implicit finally node wasn't created, then exit label and
                  finalization code must be handled here and placed before
                  afterconstruction }
                if not ((pi_needs_implicit_finally in flags) and
                  (cs_implicit_exceptions in current_settings.moduleswitches)) then
                  begin
                    include(tocode.flags,nf_block_with_exit);
                    if procdef.proctypeoption<>potype_exceptfilter then
                      addstatement(newstatement,cfinalizetempsnode.create);
                    cnodeutils.procdef_block_add_implicit_finalize_nodes(procdef,newstatement);
                    temps_finalized:=true;
                  end;

                { construction successful -> beforedestruction should be called
                  if an exception happens now }
                addstatement(newstatement,cassignmentnode.create(
                  cloadnode.create(constructionsuccessful,procdef.localst),
                  genintconstnode(1))
                );
                { Self can be nil when fail is called }
                { if self<>nil and vmt<>nil then afterconstruction }
                addstatement(newstatement,cifnode.create(
                  caddnode.create(andn,
                    caddnode.create(unequaln,
                      load_self_node,
                      cnilnode.create),
                    caddnode.create(unequaln,
                      load_vmt_pointer_node,
                      cnilnode.create)),
                    ccallnode.create(nil,tprocsym(srsym),srsym.owner,load_self_node,[],nil),
                    nil));
                tocode:=constructionblock;
              end;

            if withexceptblock and (procdef.struct.typ=objectdef) then
              begin
                { Generate the implicit "fail" code for a constructor (destroy
                  in case an exception happened) }
                pd:=tobjectdef(procdef.struct).find_destructor;
                { this will always be the case for classes, since tobject has
                  a destructor }
                if assigned(pd) or is_object(procdef.struct) then
                  begin
                    current_filepos:=exitpos;
                    exceptblock:=internalstatements(newstatement);
                    { first free the instance if non-nil }
                    if assigned(pd) then
                      { if vmt<>0 then call destructor }
                      addstatement(newstatement,
                        cifnode.create(
                          caddnode.create(unequaln,
                            load_vmt_pointer_node,
                            cnilnode.create),
                          { cnf_create_failed -> don't call BeforeDestruction }
                          ccallnode.create(nil,tprocsym(pd.procsym),pd.procsym.owner,load_self_node,[cnf_create_failed],nil),
                          nil))
                    else
                      { object without destructor, call 'fail' helper }
                      addstatement(newstatement,
                        ccallnode.createintern('fpc_help_fail',
                          ccallparanode.create(
                            cordconstnode.create(tobjectdef(procdef.struct).vmt_offset,s32inttype,false),
                          ccallparanode.create(
                            ctypeconvnode.create_internal(
                              load_vmt_pointer_node,
                              voidpointertype),
                          ccallparanode.create(
                            ctypeconvnode.create_internal(
                              load_self_pointer_node,
                              voidpointertype),
                          nil))))
                      );
                    { then re-raise the exception }
                    addstatement(newstatement,craisenode.create(nil,nil,nil));
                    current_filepos:=entrypos;
                    newblock:=internalstatements(newstatement);
                    { try
                        tocode
                      except
                        exceptblock
                      end
                    }
                    addstatement(newstatement,ctryexceptnode.create(
                      tocode,
                      nil,
                      exceptblock));
                    tocode:=newblock;
                  end;
              end;
            current_settings.localswitches:=oldlocalswitches;
          end;
      end;


    procedure tcgprocinfo.add_entry_exit_code;
      var
        finalcode,
        bodyentrycode,
        bodyexitcode,
        wrappedbody,
        newblock     : tnode;
        codestatement,
        newstatement : tstatementnode;
        oldfilepos   : tfileposinfo;
        is_constructor: boolean;
      begin
        is_constructor:=assigned(procdef.struct) and
          (procdef.proctypeoption=potype_constructor);

        oldfilepos:=current_filepos;
        { Generate code/locations used at start of proc }
        current_filepos:=entrypos;
        entry_asmnode:=casmnode.create_get_position;
        loadpara_asmnode:=casmnode.create_get_position;
        stackcheck_asmnode:=casmnode.create_get_position;
        init_asmnode:=casmnode.create_get_position;
        bodyentrycode:=generate_bodyentry_block;
        { Generate code/locations used at end of proc }
        current_filepos:=exitpos;
        exitlabel_asmnode:=casmnode.create_get_position;
        temps_finalized:=false;
        bodyexitcode:=generate_bodyexit_block;
        { Check if bodyexitcode is not empty }
        with tstatementnode(tblocknode(bodyexitcode).statements) do
          if (statement.nodetype<>nothingn) or assigned(next) then
            { Indicate that the extra code is executed after the exit statement }
            include(flowcontrol,fc_no_direct_exit);

        { Generate procedure by combining init+body+final,
          depending on the implicit finally we need to add
          an try...finally...end wrapper }
        current_filepos:=entrypos;
        newblock:=internalstatements(newstatement);
        { Note - this is not strippable since it wraps the entire procedure }
        Exclude(TBlockNode(newblock).blocknodeflags, bnf_strippable);
        { initialization is common for all cases }
        addstatement(newstatement,loadpara_asmnode);
        addstatement(newstatement,stackcheck_asmnode);
        addstatement(newstatement,entry_asmnode);
        cnodeutils.procdef_block_add_implicit_initialize_nodes(procdef,newstatement);
        addstatement(newstatement,init_asmnode);
        if assigned(procdef.parentfpinitblock) then
          begin
            if assigned(tblocknode(procdef.parentfpinitblock).left) then
              begin
                if cnodeutils.check_insert_trashing(procdef) then
                  cnodeutils.maybe_trash_variable(newstatement,tabstractnormalvarsym(procdef.parentfpstruct),cloadnode.create(procdef.parentfpstruct,procdef.parentfpstruct.owner));
                { could be an asmn in case of a pure assembler procedure,
                  but those shouldn't access nested variables }
                addstatement(newstatement,procdef.parentfpinitblock);
              end
            else
              procdef.parentfpinitblock.free;
            procdef.parentfpinitblock:=nil;
          end;
        addstatement(newstatement,bodyentrycode);

        if (cs_implicit_exceptions in current_settings.moduleswitches) and
           (pi_needs_implicit_finally in flags) and
           { but it's useless in init/final code of units }
           not(procdef.proctypeoption in [potype_unitfinalize,potype_unitinit]) and
           not(target_info.system in systems_garbage_collected_managed_types) and
           (f_exceptions in features) then
          begin
            { Any result of managed type must be returned in parameter }
            if is_managed_type(procdef.returndef) and
               (not paramanager.ret_in_param(procdef.returndef,procdef)) and
               (not is_class(procdef.returndef)) then
               InternalError(2013121301);

            { Generate special exception block only needed when
              implicit finally is used }
            current_filepos:=exitpos;
            { Generate code that will be in the try...finally }
            finalcode:=internalstatements(codestatement);
            if procdef.proctypeoption<>potype_exceptfilter then
              addstatement(codestatement,cfinalizetempsnode.create);
            cnodeutils.procdef_block_add_implicit_finalize_nodes(procdef,codestatement);
            temps_finalized:=true;

            current_filepos:=entrypos;
            wrappedbody:=ctryfinallynode.create_implicit(code,finalcode);
            { afterconstruction must be called after finalizetemps, because it
               has to execute after the temps have been finalised in case of a
               refcounted class (afterconstruction decreases the refcount
               without freeing the instance if the count becomes nil, while
               the finalising of the temps can free the instance) }
            maybe_add_constructor_wrapper(wrappedbody,true);
            addstatement(newstatement,wrappedbody);
            addstatement(newstatement,exitlabel_asmnode);
            addstatement(newstatement,bodyexitcode);
            { set flag the implicit finally has been generated }
            include(flags,pi_has_implicit_finally);
          end
        else
          begin
            { constructors need destroy-on-exception code even if they don't
              have managed variables/temps }
            maybe_add_constructor_wrapper(code,
              (cs_implicit_exceptions in current_settings.moduleswitches) and (f_exceptions in features));
            current_filepos:=entrypos;
            addstatement(newstatement,code);
            current_filepos:=exitpos;
            if assigned(nestedexitlabel) then
              addstatement(newstatement,clabelnode.create(cnothingnode.create,nestedexitlabel));
            addstatement(newstatement,exitlabel_asmnode);
            addstatement(newstatement,bodyexitcode);
            if not is_constructor then
              begin
                if procdef.proctypeoption<>potype_exceptfilter then
                  addstatement(newstatement,cfinalizetempsnode.create);
                cnodeutils.procdef_block_add_implicit_finalize_nodes(procdef,newstatement);
                temps_finalized:=true;
              end;
          end;
        if not temps_finalized then
          begin
            current_filepos:=exitpos;
            cnodeutils.procdef_block_add_implicit_finalize_nodes(procdef,newstatement);
          end;
        do_firstpass(newblock);
        code:=newblock;
        current_filepos:=oldfilepos;
      end;


    procedure clearrefs(p:TObject;arg:pointer);
      begin
         if (tsym(p).typ in [localvarsym,paravarsym,staticvarsym]) then
           if tabstractvarsym(p).refs>1 then
             tabstractvarsym(p).refs:=1;
      end;


    procedure translate_registers(p:TObject;list:pointer);
      begin
         if (tsym(p).typ in [localvarsym,paravarsym,staticvarsym]) and
            (tabstractnormalvarsym(p).localloc.loc in [LOC_REGISTER,LOC_CREGISTER,LOC_MMREGISTER,
              LOC_CMMREGISTER,LOC_FPUREGISTER,LOC_CFPUREGISTER]) then
           begin
             if not(cs_no_regalloc in current_settings.globalswitches) then
               begin
                 cg.translate_register(tabstractnormalvarsym(p).localloc.register);
                 if (tabstractnormalvarsym(p).localloc.registerhi<>NR_NO) then
                   cg.translate_register(tabstractnormalvarsym(p).localloc.registerhi);
               end;
           end;
      end;


{$if defined(i386) or defined(x86_64) or defined(arm) or defined(aarch64) or defined(riscv32) or defined(riscv64) or defined(m68k)}
    const
      exception_flags: array[boolean] of tprocinfoflags = (
        [],
        [pi_uses_exceptions,pi_needs_implicit_finally,pi_has_implicit_finally]
      );
{$endif}

    procedure tcgprocinfo.setup_tempgen;
      begin
        tg:=tgobjclass.create;

{$if defined(i386) or defined(x86_64) or defined(arm) or defined(aarch64) or defined(m68k)}
{$if defined(arm)}
        { frame and stack pointer must be always the same on arm thumb so it makes no
          sense to fiddle with a frame pointer }
        if GenerateThumbCode then
          begin
            framepointer:=NR_STACK_POINTER_REG;
            tg.direction:=1;
          end
        else
{$endif defined(arm)}
          begin
            { try to strip the stack frame }
            { set the framepointer to esp if:
              - no assembler directive, those are handled in assembler_block
                in pstatment.pas (for cases not caught by the Delphi
                exception below)
              - no exceptions are used
              - no pushes are used/esp modifications, could be:
                * outgoing parameters on the stack on non-fixed stack target
                * incoming parameters on the stack
                * open arrays
              - no inline assembler
             or
              - Delphi mode
              - assembler directive
              - no pushes are used/esp modifications, could be:
                * outgoing parameters on the stack
                * incoming parameters on the stack
                * open arrays
              - no local variables

              - stack frame cannot be optimized if using Win64 SEH
                (at least with the current state of our codegenerator).
            }
            if ((po_assembler in procdef.procoptions) and
               (m_delphi in current_settings.modeswitches) and
               { localst at main_program_level is a staticsymtable }
                (procdef.localst.symtablelevel<>main_program_level) and
                (tabstractlocalsymtable(procdef.localst).count_locals = 0)) or
               ((cs_opt_stackframe in current_settings.optimizerswitches) and
                not(cs_generate_stackframes in current_settings.localswitches) and
                not(cs_profile in current_settings.moduleswitches) and
                not(po_assembler in procdef.procoptions) and
{$if defined(m68k)}
                { do not optimize away the frame pointer, if the CPU has no long
                  displacement support, this fixes optimizations on the plain 68000
                  until some shortcomings of the CG itself can be addressed. (KB) }
                (CPUM68K_HAS_BASEDISP in cpu_capabilities[current_settings.cputype]) and
{$endif defined(m68k)}
{$if defined(aarch64)}
               { on aarch64, it must be a leaf subroutine }
                not(pi_do_call in flags) and
{$endif defined(aarch64)}
                not ((pi_has_stackparameter in flags)
{$if defined(i386) or defined(x86_64)}
               { Outgoing parameter(s) on stack do not need stackframe on x86 targets
                 with fixed stack. On ARM it fails, see bug #25050 }
                  and (not paramanager.use_fixed_stack)
{$endif defined(i386) or defined(x86_64)}
                  ) and
                ((flags*([pi_has_assembler_block,pi_is_assembler,
                        pi_needs_stackframe]+
                        exception_flags[((target_info.cpu=cpu_i386) and (not paramanager.use_fixed_stack))
{$ifndef DISABLE_WIN64_SEH}
                        or (target_info.system in systems_x86_64_seh)
{$endif DISABLE_WIN64_SEH}
                        ]))=[])
               )
            then
              begin
                { we need the parameter info here to determine if the procedure gets
                  parameters on the stack

                  calling generate_parameter_info doesn't hurt but it costs time
                  (necessary to init para_stack_size)
                }
                generate_parameter_info;

                if not(procdef.stack_tainting_parameter(calleeside)) and
                   not(has_assembler_child)
                  { parasize must be really zero, this means also that no result may be returned
                    in a parameter }
                  and not((current_procinfo.procdef.proccalloption in clearstack_pocalls) and
                  not(current_procinfo.procdef.generate_safecall_wrapper) and
                  paramanager.ret_in_param(current_procinfo.procdef.returndef,current_procinfo.procdef)) then
                  begin
                    { Only need to set the framepointer }
                    framepointer:=NR_STACK_POINTER_REG;
                    tg.direction:=1;
                    Include(flags,pi_no_framepointer_needed)
                  end
{$if defined(arm)}
                { On arm, the stack frame size can be estimated to avoid using an extra frame pointer,
                  in case parameters are passed on the stack.

                  However, the draw back is, if the estimation fails, compilation will break later on
                  with an internal error, so this switch is not enabled by default yet. To overcome this,
                  multipass compilation of subroutines must be supported
                }
                else if (cs_opt_forcenostackframe in current_settings.optimizerswitches) and
                   not(has_assembler_child) then
                  begin
                    { Only need to set the framepointer }
                    framepointer:=NR_STACK_POINTER_REG;
                    tg.direction:=1;
                    include(flags,pi_estimatestacksize);
                    set_first_temp_offset;
                    procdef.has_paraloc_info:=callnoside;
                    generate_parameter_info;
                    exit;
                  end;
{$endif defined(arm)}
              end;
          end;
{$endif defined(x86) or defined(arm) or defined(m68k)}
{$if defined(xtensa)}
        { On xtensa, the stack frame size can be estimated to avoid using an extra frame pointer,
          in case parameters are passed on the stack.

          However, the draw back is, if the estimation fails, compilation will break later on
          with an internal error, so this switch is not enabled by default yet. To overcome this,
          multipass compilation of subroutines must be supported
        }
        if procdef.stack_tainting_parameter(calleeside) then
          begin
            include(flags,pi_estimatestacksize);
            set_first_temp_offset;
            procdef.has_paraloc_info:=callnoside;
            generate_parameter_info;
            exit;
          end;
{$endif defined(xtensa)}
        { set the start offset to the start of the temp area in the stack }
        set_first_temp_offset;
      end;


    { recursively gather the procdef and (parsed) code tree of every nested
      routine of pi into the parallel lists defs/bodies, for
      CollectNestedProcDefSyms.  Nested routines are parsed with their parent,
      so their code trees are available when the parent's DFA runs (the parent's
      generate_code precedes generate_code_tree's descent into the nest). }
    procedure collect_nested_bodies(pi : tprocinfo;defs,bodies : tfplist);
      var
        hpi : tprocinfo;
      begin
        hpi:=pi.get_first_nestedproc;
        while assigned(hpi) do
          begin
            if assigned(tcgprocinfo(hpi).code) and
               not(df_generic in hpi.procdef.defoptions) then
              begin
                defs.Add(hpi.procdef);
                bodies.Add(tcgprocinfo(hpi).code);
              end;
            collect_nested_bodies(hpi,defs,bodies);
            hpi:=tprocinfo(hpi.next);
          end;
      end;


    procedure tcgprocinfo.TransformNodeTree;
      var
        i : integer;
        UserCode : TNode;
        updated,
        RedoDFA : boolean;
        loopfillsyms : tfplist;
        guardsyms : tfplist;
        nestedsyms : tfplist;
        nesteddefs,nestedbodies : tfplist;
      begin
       loopfillsyms:=tfplist.Create;
       guardsyms:=tfplist.Create;
       nestedsyms:=tfplist.Create;
       { inlining is a heuristics, so we do this very early }
       do_optinline(code,updated);

       { do this before adding the entry code else the tail recursion recognition won't work,
         if this causes troubles, it must be if'ed
       }
       if (cs_opt_tailrecursion in current_settings.optimizerswitches) and
         (pi_is_recursive in flags) then
         do_opttail(code,procdef);

       { scalar replacement of aggregates: split a non-escaping local record
         variable into one scalar temporary per field and rewrite every
         rec.field access to its temp, so the fields become plain scalar locals
         the passes below (constant propagation, DFA, dead-store elimination)
         handle in registers instead of through the stack frame. Run first so
         those passes see the synthesized scalar temps; needs no DFA (its checks
         are a structural escape walk). Skipped for routines with inline
         assembler (the record's storage may be referenced opaquely). }
       if (cs_opt_sra in current_settings.optimizerswitches) and
         ((flags*[pi_has_assembler_block,pi_is_assembler])=[]) then
         OptimizeSRA(code,procdef);

       if cs_opt_constant_propagate in current_settings.optimizerswitches then
         begin
           do_optconstpropagate(code,RedoDFA);
           { RedoDFA value not used here }
           RedoDFA:=false;
         end;

       { final value replacement + dead loop elimination (the gcc
         -ftree-scev-cprop idea plus whole-loop deletion): when a counted
         for-loop's whole body is a single loop-invariant accumulator update
         of a plain local integer and the counter's exit value is dead,
         replace the loop by the closed form of the accumulator's exit value
         and delete the loop, so post-loop uses read the closed form directly.
         Runs HERE, before the DFA/loop passes and ConvertForLoops rewrite or
         lower the still-structured for-nodes, so the counter bounds are read
         directly; needs no DFA (a structural whole-tree check). Skips
         routines with labels (goto could enter the loop), inline assembler
         and exceptions. Opt-in via -OoFINALVALUE; disabled under -Co/-Cr. }
       if (cs_opt_finalvalue in current_settings.optimizerswitches) and
         ((flags*[pi_has_assembler_block,pi_is_assembler,pi_uses_exceptions,pi_has_label])=[]) then
         OptimizeFinalValue(code);

       { provable-receiver devirtualization (-OoDEVIRT): rewrite virtual calls
         whose receiver a conservative constructor-provenance analysis proves
         monomorphic into direct calls to the concrete override (see optdevirt).
         Skipped for routines with inline assembler (the receiver's storage may
         be referenced opaquely) or with labels (goto could enter regions the
         provenance scan assumed unreachable). Independent of DFA -- it is a
         structural whole-tree scan. This runs AFTER do_optinline above, so a
         call rebound here toward an inlineable override (OptimizeDevirt returns
         true) is fed back through do_optinline once more, letting the inliner
         expand the now-direct target in this same routine; the freshly inlined
         body is still seen by the DFA/constprop/loop passes that follow. }
       if (cs_opt_devirt in current_settings.optimizerswitches) and
         ((flags*[pi_has_assembler_block,pi_is_assembler,pi_has_label])=[]) then
         begin
           if OptimizeDevirt(code) and
              (cs_do_inline in current_settings.localswitches) then
             do_optinline(code,updated);
         end;

       if (cs_opt_nodedfa in current_settings.optimizerswitches) and
         { creating dfa is not always possible }
         ((flags*[pi_has_assembler_block,pi_uses_exceptions,pi_is_assembler])=[]) then
         begin
           dfabuilder:=TDFABuilder.Create;
           dfabuilder.createdfainfo(code);
           include(flags,pi_dfaavailable);
           RedoDFA:=false;

           { record local/static arrays that are element-filled and element-read
             by matched counted for-loops, on the still-structured for-node tree
             (ConvertForLoops below lowers the for-nodes to while-loops).  Their
             later DFA "does not seem to be initialized" warning is a false
             positive; skip it in the warning loop.  Diagnostic only -- liveness
             and noregvarinitneeded are untouched, so codegen is unaffected. }
           CollectLoopFillCoveredSyms(code,loopfillsyms);

           { record local/parameter scalars that are read only under a
             correlated if-guard that provably dominates them (see
             CollectCorrelatedGuardSyms).  Their later DFA "does not seem to be
             initialized" warning is a false positive; skip it in the warning
             loop.  Only done for routines without labels/goto/exceptions
             (guaranteed linear control flow within a statement list, so no edge
             can enter the second guard without the first).  Diagnostic only --
             liveness and noregvarinitneeded are untouched, so codegen is
             unaffected. }
           if (flags*[pi_has_assembler_block,pi_is_assembler,pi_uses_exceptions,pi_has_label])=[] then
             CollectCorrelatedGuardSyms(code,guardsyms);

           { record locals of this routine that are assigned only inside a
             nested routine which the routine calls before reading them.  The
             DFA does not model a nested-procedure call as a definition of the
             captured parent local, so its later "does not seem to be
             initialized" warning is a false positive; skip it in the warning
             loop.  Diagnostic only -- liveness and noregvarinitneeded are
             untouched, so codegen is unaffected. }
           if has_nestedprocs and assigned(procdef.localst) then
             begin
               nesteddefs:=tfplist.Create;
               nestedbodies:=tfplist.Create;
               try
                 collect_nested_bodies(self,nesteddefs,nestedbodies);
                 CollectNestedProcDefSyms(code,procdef.localst,nesteddefs,nestedbodies,nestedsyms);
               finally
                 nesteddefs.Free;
                 nestedbodies.Free;
               end;
             end;

           if cs_opt_constant_propagate in current_settings.optimizerswitches then
             begin
               do_optconstpropagate(code,RedoDFA);
               if RedoDFA then
                 begin
                   dfabuilder.redodfainfo(code);
                   RedoDFA:=false; { Don't redo it again unless necessary }
                 end;
               { Don't re-run constant propagation as redoing DFA info didn't
                 actually change any nodes }
             end;

           { interval value-range propagation with branch folding: forward-
             propagate integer value INTERVALS -- seeded from a variable's
             declared subrange/ordinal type bounds, from for-loop counter
             constant bounds, and from straight-line const/Length/mod/and facts
             -- and fold every user-level if whose comparison the intervals
             already decide, deleting the dead arm. Generalizes -OoRANGEELIM's
             range analysis (which only removes -Cr check nodes) into a control-
             flow consumer, distinct from -OoJUMPTHREAD (which seeds only from
             dominating branch conditions). Runs FIRST among the loop passes,
             BEFORE loop-splitting/peeling/lowering rewrite the still-structured
             for-nodes' constant bounds into temps, so the counter interval can
             be read directly; removes branches, so refresh DFA afterwards; skips
             procedures with labels like the loop passes below. }
           if (cs_opt_vrp in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeVRP(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { loop splitting: when a counted for-loop's whole body is a single if
             comparing the induction variable against a loop-invariant bound
             (if i<m then A else B), split the iteration space at the crossover
             into two consecutive branch-free loops. Run before the vectorizer so
             the branch-free interior loop it exposes becomes a vectorizable
             uniform kernel; needs valid DFA to prove the counter/bound unmodified
             in the body, and skips procedures with labels like the loop passes
             below so control cannot enter a split loop mid-stream. }
           if (cs_opt_loopsplit in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeLoopSplit(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { loop fusion: merge two adjacent counted for-loops over the same
             iteration space into one loop body when no dependence forbids it, so
             an intermediate result stays in registers/cache instead of being
             streamed out by the first loop and reloaded from memory by the
             second. Run before the vectorizer so a fused element-wise loop is
             still presented as a single for-node the vectorizer can pack, and
             before strength reduction / the for->while lowering (it matches on
             for-nodes with plain a[i] index nodes). Needs valid DFA to prove each
             counter is not modified in its body; skips procedures with labels
             like the loop passes below so control cannot enter a fused body
             mid-stream. }
           if (cs_opt_loopfuse in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeLoopFuse(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { unroll-and-jam: unroll the outer loop of a perfect two-level counted
             nest by a small factor and fuse (jam) the duplicated inner-loop
             bodies into one inner loop, so a value the inner body loads once
             (b[j] in a matmul-shaped nest) is reused across the unrolled outer
             iterations from a register and a per-outer-iteration scalar
             accumulator is register-blocked. Run before the vectorizer / strength
             reduction / the for->while lowering: it matches on for-nodes with
             plain a[i] index nodes and copies the outer body with the counter
             shifted by i+1..i+K-1, which needs the counter to still appear as a
             plain read. Needs valid DFA to prove the outer counter is not modified
             in its body; skips procedures with labels like the loop passes below
             so control cannot enter a jammed body mid-stream. }
           if (cs_opt_unrolljam in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeUnrollJam(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { predictive commoning: in a counted loop that reads B[i+c] for a small
             window of constant offsets c, carry the window in a rotating set of
             scalar temporaries and load only the leading edge B[i+maxoff] each
             iteration instead of re-loading every offset (the stencil / 1-D
             convolution sliding window). Run before the vectorizer / strength
             reduction / the for->while lowering: it matches on for-nodes with
             plain B[i+c] index nodes, which strength reduction would rewrite into
             pointer walks the recognizer can't match. Needs valid DFA to prove the
             counter is not modified in its body; skips procedures with labels like
             the loop passes below so control cannot enter a partly-rotated body
             mid-stream. }
           if (cs_opt_predcom in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizePredCom(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { code sinking (the symmetric counterpart of LICM): move a pure,
             side-effect-free assignment  V:=<expr>  that immediately precedes
             an if and whose value is consumed on only ONE arm (and is dead on
             the fall-through after the if) down into that arm, so paths that
             never use V stop computing it -- partially dead code elimination.
             Needs valid (freshly redone) DFA to read the if-successor's life
             set that proves V dead after the branch; skips procedures with
             labels like the loop passes so a goto cannot land between the
             assignment and the arm it was sunk into. }
           if (cs_opt_sink in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeCodeSink(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { conservative loop autovectorization: rewrite a counted single-
             precision element-wise for-loop  for i:=lo to hi do a[i]:=b[i] op c[i]
             into a 128-bit SSE packed main loop (4 singles/iteration) plus a
             scalar remainder loop. Run before strength reduction so the array
             accesses are still plain vecn index nodes -- strength reduction
             would rewrite them into pointer walks the recognizer can't match.
             Needs valid DFA to prove the counter and arrays are not modified in
             the body; skips procedures with labels like the loop passes below so
             control cannot enter the rewritten loop mid-stream.
             The same recognizer/scaffold also drives -OoIFCONVERT: a counted loop
             whose body FPC's -O2 if-conversion has already lowered to a branch-
             free single-precision min/max activation (ReLU / one-sided clamp /
             element-wise max-min) is widened to a packed maxps/minps main loop,
             so the enabling gate is either switch. }
           if (([cs_opt_vectorize,cs_opt_ifconvert]*current_settings.optimizerswitches)<>[])
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeVectorize(code) or RedoDFA;

           { dynamic-trip loop unrolling + software prefetch: unroll a counted
             for-loop of unknown trip count by 4 (four serial body copies + a
             scalar remainder loop) and/or insert a PREFETCHNTA of base[i+DIST]
             per iteration group for each streamed dynamic-array base. Targets
             the long bandwidth-bound array-walk loops the stock -OoLOOPUNROLL
             (constant trip counts only) never fires on. Runs after the
             vectorizer (which consumes the for-node when it fires, so the two do
             not both rewrite the same loop) and before strength reduction, while
             the a[i] index nodes are still plain vecn reads. Needs valid DFA to
             prove the counter and streamed bases are not modified in the body;
             skips procedures with labels like the loop passes so control cannot
             enter the rewritten loop mid-stream. }
           if (([cs_opt_unrolldyn,cs_opt_prefetch]*current_settings.optimizerswitches)<>[])
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeUnrollPrefetch(code) or RedoDFA;

           { SLP (superword-level parallelism) straight-line vectorization:
             pack runs of >=4 adjacent, isomorphic scalar single-precision
             element-wise assignments over consecutive array slots (hand-unrolled
             code with no surrounding loop, which the loop vectorizer above never
             sees) into 128-bit SSE packed ops, reusing the loop vectorizer's
             backend node. Purely syntactic (needs no DFA) but creates temps, so
             it invalidates DFA like the loop passes; run before strength
             reduction so the a[k] accesses are still plain vecn index nodes, and
             skip procedures with labels like the loop passes so control cannot
             enter a packed group mid-stream. }
           if (cs_opt_slp in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeSLP(code) or RedoDFA;

           { loop-distribution pattern idiom recognition: lower a counted
             for-loop whose whole body is a contiguous fill/zero/copy over an
             array region into a FillChar/FillWord/FillDWord/FillQWord/Move block
             primitive. Run before strength reduction (which would rewrite the
             a[i] index nodes into pointer walks the recognizer can't match) and
             before the for->while lowering below (it matches on for-nodes).
             Needs valid DFA to prove the counter is not modified in the body;
             skips procedures with labels like the loop passes here so control
             cannot enter the rewritten loop mid-stream. }
           if (cs_opt_loopdistpat in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeLoopDistPat(code) or RedoDFA;

           { loop peeling: fully unroll a counted for-loop whose trip count is a
             small compile-time constant into straight-line copies of the body,
             folding the induction variable to its per-iteration constant and
             deleting the loop control. Run here (still on for-nodes, before the
             for->while lowering below and before strength reduction rewrites the
             a[i] index nodes) so the peeled copies feed later constant
             propagation. Needs valid DFA to prove the counter is unmodified in
             the body; skips procedures with labels like the loop passes here so
             control cannot enter a peeled copy mid-stream. }
           if (cs_opt_looppeel in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeLoopPeel(code) or RedoDFA;

           { reduction reassociation: split the single serial FP/integer
             accumulator of a  for i:=lo to hi do acc:=acc+expr  sum / dot-product
             reduction into K independent partial accumulators combined after the
             loop, breaking the loop-carried dependency chain. FP accumulators are
             only reassociated under fast-math. Run here (still on for-nodes,
             before the for->while lowering below and BEFORE strength reduction
             rewrites the a[i] index nodes into pointer walks -- the pass copies
             the body with the counter shifted by i+1..i+3, which needs the
             counter to still appear as a plain read). Skips procedures with labels
             like the loop passes here so control cannot enter a split body mid-
             stream. Uses no DFA (its checks are structural), but its new nodes are
             folded into RedoDFA so strength reduction below sees fresh info. }
           if (cs_opt_reassoc in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeReassoc(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           if (cs_opt_loopstrength in current_settings.optimizerswitches)
             { our induction variable strength reduction doesn't like
               for loops with more than one entry }
             and not(pi_has_label in flags) then
             begin
               RedoDFA:=OptimizeInductionVariables(code);
             end;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false; { Don't redo it again unless necessary }
             end;

           { loop unswitching: hoist a loop-invariant conditional out of a loop
             and clone the loop into branch-free then/else variants. Run before
             LICM so the clones it produces are then exposed to invariant
             hoisting; needs valid DFA to prove the condition invariant and,
             like strength reduction and LICM, we skip procedures with labels. }
           if (cs_opt_loopunswitch in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeLoopUnswitch(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               { refresh DFA so LICM sees fresh info on the cloned loops }
               RedoDFA:=false;
             end;

           { loop-invariant code motion; needs valid DFA, and (like strength
             reduction) we skip procedures containing labels }
           if (cs_opt_loopmotion in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeLICM(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { bit-population-count idiom recognition: rewrite the scalar
             clear-lowest-set-bit loop into the PopCnt intrinsic. Pure node
             pattern-match (does not use DFA); skips procedures with labels
             like the loop passes above so control never enters mid-rewrite. }
           if (cs_opt_bitidiom in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeBitIdiom(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { value-range range-check elimination: drop the -Cr per-access array
             bounds check on a[i] inside a counted for-loop when the counter i
             is provably an in-bounds index (static array with constant bounds
             inside the array, or dynamic array bounded by its own high()).
             Needs valid DFA to prove the counter (and, for the dynamic case,
             the array) are not modified in the body; skips procedures with
             labels like the loop passes above so control cannot enter the loop
             with i out of range. It only clears the range-check localswitch on
             qualifying nodes -> no CFG change, so no DFA refresh is needed. }
           if (cs_opt_rangecheckelim in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             OptimizeRangeElim(code);

           { jump threading / nested re-test elimination: fold a nested if whose
             condition a dominating branch (or a value-range fact on a simple
             unmodified variable) already decided straight to the taken arm,
             deleting the redundant re-test and its dead arm. Restructures the
             CFG (removes branches), so refresh DFA afterwards; skips procedures
             with labels like the loop passes above so control cannot enter a
             threaded region mid-way. }
           if (cs_opt_jumpthread in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeJumpThread(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { loop store motion / scalar promotion (the store-side counterpart of
             LICM): when a loop repeatedly loads/stores an invariant-address
             memory location (v1: a plain unmanaged global written in the loop),
             promote it to a register temp -- load once before, operate on the
             temp inside, store back once after. Pure node-pattern rewrite (does
             not use DFA); runs on the still-structured for/while nodes BEFORE
             ConvertForLoops lowers them, so the body is clean user code. Skips
             procedures with labels like the loop passes above so control cannot
             enter the loop between the pre-load and the post-store. }
           if (cs_opt_storemotion in current_settings.optimizerswitches)
             and not(pi_has_label in flags) then
             RedoDFA:=OptimizeStoreMotion(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           { switch-to-lookup-table conversion (gcc -ftree-switch-conversion,
             the static-table half, complementing -OoCASECLUSTER which only
             optimises the dispatch): when every arm of a fully-covered (no-hole)
             case over an ordinal only assigns compile-time constants to the same
             ordered set of simple ordinal variables, replace the whole statement
             -- dispatch and bodies -- with per-variable static const arrays
             indexed by (selector-low) plus a single range guard jumping to the
             else part, eliminating all branching. The selector is evaluated once
             into a temp before any store, so it stays correct even with side
             effects or a selector that is itself a store target. Removes
             branches, so refresh DFA afterwards. Case arms cannot be entered by
             a goto, so it is safe in the presence of labels. }
           if (cs_opt_switchtable in current_settings.optimizerswitches) then
             RedoDFA:=OptimizeSwitchTable(code) or RedoDFA;

           if RedoDFA then
             begin
               dfabuilder.redodfainfo(code);
               RedoDFA:=false;
             end;

           if cs_opt_forloop in current_settings.optimizerswitches then
             RedoDFA:=OptimizeForLoop(code);

           RedoDFA:=ConvertForLoops(code) or RedoDFA;

           if cs_opt_forloop in current_settings.optimizerswitches then
             RedoDFA:=optimize_record_writes(code) or RedoDFA;

           if RedoDFA then
             dfabuilder.redodfainfo(code);

           { when life info is available, we can give more sophisticated warning about uninitialized
             variables ...
             ... but not for the finalization section of a unit, we would need global dfa to handle
             it properly }
           if potype_unitfinalize<>procdef.proctypeoption then
             { iterate through life info of the first node }
             for i:=0 to dfabuilder.nodemap.count-1 do
               begin
                 UserCode:=GetUserCode();
                 if DynSetIn(UserCode.optinfo^.life,i) then
                   begin
                     { do not warn for certain parameters: }
                     if not((tnode(dfabuilder.nodemap[i]).nodetype=loadn) and (tloadnode(dfabuilder.nodemap[i]).symtableentry.typ=paravarsym) and
                       { do not warn about parameters passed by var }
                       (((tparavarsym(tloadnode(dfabuilder.nodemap[i]).symtableentry).varspez=vs_var) and
                       { function result is passed by var but it must be initialized }
                       not(vo_is_funcret in tparavarsym(tloadnode(dfabuilder.nodemap[i]).symtableentry).varoptions)) or
                       { do not warn about initialized hidden parameters }
                       ((tparavarsym(tloadnode(dfabuilder.nodemap[i]).symtableentry).varoptions*[vo_is_high_para,vo_is_parentfp,vo_is_result,vo_is_self])<>[]))) and
                       { skip matched loop-fill false positives (see above) }
                       not((tnode(dfabuilder.nodemap[i]).nodetype=loadn) and
                           (loopfillsyms.IndexOf(tloadnode(dfabuilder.nodemap[i]).symtableentry)>=0)) and
                       { skip correlated if-guard false positives (see above) }
                       not((tnode(dfabuilder.nodemap[i]).nodetype=loadn) and
                           (guardsyms.IndexOf(tloadnode(dfabuilder.nodemap[i]).symtableentry)>=0)) and
                       { skip nested-procedure-def false positives (see above) }
                       not((tnode(dfabuilder.nodemap[i]).nodetype=loadn) and
                           (nestedsyms.IndexOf(tloadnode(dfabuilder.nodemap[i]).symtableentry)>=0)) then
                       CheckAndWarn(UserCode,tnode(dfabuilder.nodemap[i]));
                   end
                 else
                   begin
                     if (tnode(dfabuilder.nodemap[i]).nodetype=loadn) and
                       (tloadnode(dfabuilder.nodemap[i]).symtableentry.typ in [staticvarsym,localvarsym]) then
                       tabstractnormalvarsym(tloadnode(dfabuilder.nodemap[i]).symtableentry).noregvarinitneeded:=true
                   end;
               end;

           if cs_opt_dead_store_eliminate in current_settings.optimizerswitches then
             begin
               if normalize(code) then
                 begin
                   do_optdeadstoreelim(code,RedoDFA);
                   if RedoDFA then
                     dfabuilder.redodfainfo(code);
                 end;
             end;
         end
       else
         begin
           ConvertForLoops(code);
           if cs_opt_forloop in current_settings.optimizerswitches then
             optimize_record_writes(code);
         end;

       if (cs_opt_remove_empty_proc in current_settings.optimizerswitches) and
         (procdef.proctypeoption in [potype_operator,potype_procedure,potype_function]) and
         (code.nodetype=blockn) and (tblocknode(code).statements=nil) then
         procdef.isempty:=true;

       { interprocedural pure/const attribute discovery (the gcc
         -fipa-pure-const idea): record on this routine's procdef whether it is
         provably const (result depends only on its by-value parameters) and/or
         pure (reads but never writes global state, no I/O / side effects), so
         that later-compiled callers' LICM can hoist a call to it out of a loop.
         Runs here, on the final node tree of the current routine, so its
         summary is available to every routine compiled after it in the unit;
         mutually-recursive SCCs are resolved by the on-demand fixpoint in
         optpure. Opt-in via -OoPURE; the flags default to "impure" for routines
         we never analysed (e.g. loaded from other units). }
       if cs_opt_pure in current_settings.optimizerswitches then
         AnalyzeProcPurity(procdef,code);

       { global value numbering + full-redundancy elimination: number
         side-effect-free scalar expressions across control flow and reuse a
         value already available on every path (computed on a dominating
         statement / before a branch and reused on the rejoining arms, or
         recomputed across straight-line bodies) from a temp instead of
         recomputing it. Complements the intra-expression CSE (do_optcse, run
         right after) with redundancy ACROSS statements and rejoining branches;
         runs here in the same late node-tree phase, after the loop recognizers
         and ConvertForLoops so it never disturbs their still-structured
         for-nodes. Skips procedures with labels, inline assembler or exceptions
         (partial-evaluation / regvar hazards); opt-in via -OoGVNPRE. }
       if (cs_opt_gvnpre in current_settings.optimizerswitches) and
         ((flags*[pi_has_assembler_block,pi_is_assembler,pi_uses_exceptions,pi_has_label])=[]) then
         OptimizeGVNPRE(code);

       if cs_opt_nodecse in current_settings.optimizerswitches then
         do_optcse(code);

       if cs_opt_use_load_modify_store in current_settings.optimizerswitches then
         do_optloadmodifystore(code);

       if (cs_opt_consts in current_settings.optimizerswitches) and
          { non-local gotos can cause an fpc_setjmp call to be generated before
            this block, which means the loaded value won't be loaded when the
             longjmp is performed }
          not(m_non_local_goto in current_settings.modeswitches) then
         do_consttovar(code);

       loopfillsyms.Free;
       guardsyms.Free;
       nestedsyms.Free;
      end;


    function tcgprocinfo.has_assembler_child : boolean;
      var
        hp : tprocinfo;
      begin
        result:=false;
        hp:=get_first_nestedproc;
        while assigned(hp) do
          begin
            if (hp.flags*[pi_has_assembler_block,pi_is_assembler])<>[] then
              begin
                result:=true;
                exit;
              end;
            hp:=tprocinfo(hp.next);
          end;
      end;


    procedure tcgprocinfo.set_eh_info;
      begin
        inherited;
         if (tf_use_psabieh in target_info.flags) and
            ((pi_uses_exceptions in flags) or
             ((cs_implicit_exceptions in current_settings.moduleswitches) and
              (pi_needs_implicit_finally in flags))) or
             (pi_has_except_table_data in flags) then
           procdef.personality:=search_system_proc('_FPC_PSABIEH_PERSONALITY_V0');
      end;


    function tcgprocinfo.store_node_tempflags(var n: tnode; arg: pointer): foreachnoderesult;
      var
        nodeset : THashSet absolute arg;
        entry : ptempinfo_flags_entry;
        i : longint;
        {hashsetitem: PHashSetItem;}
      begin
        result:=fen_true;
        case n.nodetype of
          tempcreaten:
            begin
              {$ifdef EXTDEBUG}
              comment(V_Debug,'keeping track of new temp node: '+hexstr(ttempbasenode(n).tempinfo));
              {$endif EXTDEBUG}
              nodeset.FindOrAdd(ttempbasenode(n).tempinfo,sizeof(pointer));
            end;
          tempdeleten:
            begin
              {$ifdef EXTDEBUG}
              comment(V_Debug,'got temp delete node: '+hexstr(ttempbasenode(n).tempinfo));
              {$endif EXTDEBUG}
              { don't remove temp nodes so that outside code can know if some temp
                was only created in here }
              (*hashsetitem:=nodeset.find(ttempbasenode(n).tempinfo,sizeof(pointer));
              if assigned(hashsetitem) then
                begin
                  {$ifdef EXTDEBUG}
                  comment(V_Debug,'no longer keeping track of temp node');
                  {$endif EXTDEBUG}
                  writeln('no longer keeping track of temp node');
                  nodeset.Remove(hashsetitem);
                end;*)
            end;
          temprefn:
            begin
              {$ifdef EXTDEBUG}
              comment(V_Debug,'found temp ref node: '+hexstr(ttempbasenode(n).tempinfo));
              {$endif EXTDEBUG}
              if not assigned(nodeset.find(ttempbasenode(n).tempinfo,sizeof(pointer))) then
                begin
                  for i:=0 to tempinfo_flags_map.count-1 do
                    begin
                      entry:=ptempinfo_flags_entry(tempinfo_flags_map[i]);
                      {$ifdef EXTDEBUG}
                      comment(V_Debug,'comparing with tempinfo: '+hexstr(entry^.tempinfo));
                      {$endif EXTDEBUG}
                      if entry^.tempinfo=ttempbasenode(n).tempinfo then
                        begin
                          {$ifdef EXTDEBUG}
                          comment(V_Debug,'temp node exists');
                          {$endif EXTDEBUG}
                          exit;
                        end;
                    end;
                  {$ifdef EXTDEBUG}
                  comment(V_Debug,'storing node');
                  {$endif EXTDEBUG}
                  new(entry);
                  entry^.tempinfo:=ttempbasenode(n).tempinfo;
                  entry^.flags:=ttempinfoaccessor.gettempinfoflags(entry^.tempinfo);
                  tempinfo_flags_map.add(entry);
                end
              else
                begin
                  {$ifdef EXTDEBUG}
                  comment(V_Debug,'ignoring node');
                  {$endif EXTDEBUG}
                end;
            end;
          else
            ;
        end;
      end;


    procedure tcgprocinfo.store_tempflags;
      var
        nodeset : THashSet;
      begin
        if assigned(tempinfo_flags_map) then
          internalerror(2020040601);
        {$ifdef EXTDEBUG}
        comment(V_Debug,'storing temp nodes of '+procdef.mangledname);
        {$endif EXTDEBUG}
        tempinfo_flags_map:=tfplist.create;
        nodeset:=THashSet.Create(32,false,false);
        foreachnode(code,@store_node_tempflags,nodeset);
        nodeset.free;
        nodeset := nil;
      end;


    procedure tcgprocinfo.swap_tempflags;
      var
        entry : ptempinfo_flags_entry;
        i : longint;
        tempflags : ttempinfoflags;
      begin
        if not assigned(tempinfo_flags_map) then
          exit;
        for i:=0 to tempinfo_flags_map.count-1 do
          begin
            entry:=ptempinfo_flags_entry(tempinfo_flags_map[i]);
            tempflags:=ttempinfoaccessor.gettempinfoflags(entry^.tempinfo);
            ttempinfoaccessor.settempinfoflags(entry^.tempinfo,entry^.flags);
            entry^.flags:=tempflags;
          end;
      end;


    procedure tcgprocinfo.apply_tempflags;
      begin
        if tempflags_swapped then
          internalerror(2020040602);
        swap_tempflags;
        tempflags_swapped:=true;
      end;


    procedure tcgprocinfo.reset_tempflags;
      begin
        if not tempflags_swapped then
          internalerror(2020040603);
        swap_tempflags;
        tempflags_swapped:=false;
      end;


{$ifdef DEBUG_NODE_XML}
    procedure tcgprocinfo.XMLPrintProc(FirstHalf: Boolean);
      var
        T: Text;
        W: Word;
        syssym: tsyssym;
        separate : boolean;

      procedure PrintType(Flag: string);
        begin
          if df_generic in procdef.defoptions then
            Write(T, ' type="generic ', Flag, '"')
          else
            Write(T, ' type="', Flag, '"');
        end;

      procedure PrintOption(Flag: string);
        begin
          WriteLn(T, PrintNodeIndention, '<option>', Flag, '</option>');
        end;

      begin
        if current_module.ppxfilefail then
          Exit;

        Assign(T, current_module.ppxfilename);
        {$push} {$I-}
        Append(T);
        if IOResult <> 0 then
          begin
            Message1(exec_e_cant_create_archivefile,current_module.ppxfilename);
            current_module.ppxfilefail := True;
            Exit;
          end;
        {$pop}

        separate := (df_generic in procdef.defoptions);

        { First half prints the header and the nodes as a "code" tag }
        if FirstHalf or separate then
          begin
            Write(T, PrintNodeIndention, '<subroutine');
            { Check to see if the procedure is a class or object method }
            if Assigned(procdef.struct) then
              begin
                if Assigned(procdef.struct.objrealname) then
                  Write(T, ' struct="', SanitiseXMLString(procdef.struct.objrealname^), '"')
                else
                  Write(T, ' struct="&lt;NULL&gt;"');
              end;
            case procdef.proctypeoption of
              potype_none:
                { Do nothing - should this be an internal error though? };
              potype_procedure,
              potype_function:
                if po_classmethod in procdef.procoptions then
                  begin
                    if po_staticmethod in procdef.procoptions then
                      PrintType('static class method')
                    else
                      PrintType('class method');
                  end
                else if df_generic in procdef.defoptions then
                  Write(T, ' type="generic"');
              potype_proginit,
              potype_unitinit:
                PrintType('initialization');
              potype_unitfinalize:
                PrintType('finalization');
              potype_constructor:
                PrintType('constructor');
              potype_destructor:
                PrintType('destructor');
              potype_operator:
                PrintType('operator');
              potype_class_constructor:
                PrintType('class constructor');
              potype_class_destructor:
                PrintType('class destructor');
              potype_propgetter:
                PrintType('dispinterface getter');
              potype_propsetter:
                PrintType('dispinterface setter');
              potype_exceptfilter:
                PrintType('except filter');
              potype_mainstub:
                PrintType('main stub');
              potype_libmainstub:
                PrintType('library main stub');
              potype_pkgstub:
                PrintType('package stub');
            end;

            Write(T, ' name="', SanitiseXMLString(procdef.customprocname([pno_showhidden, pno_noclassmarker])), '"');
            if (po_hascallingconvention in procdef.procoptions) or (procdef.proccalloption <> pocall_default) then
              Write(T, ' convention="', proccalloptionStr[procdef.proccalloption], '"');
            WriteLn(T, '>');

            PrintNodeIndent;

            if Assigned(procdef.returndef) and not is_void(procdef.returndef) then
              WriteLn(T, PrintNodeIndention, '<returndef>', SanitiseXMLString(procdef.returndef.typesymbolprettyname), '</returndef>');

            if po_reintroduce in procdef.procoptions then
              PrintOption('reintroduce');
            if po_virtualmethod in procdef.procoptions then
              PrintOption('virtual');
            if po_finalmethod in procdef.procoptions then
              PrintOption('final');
            if po_overridingmethod in procdef.procoptions then
              PrintOption('override');
            if po_overload in procdef.procoptions then
              PrintOption('overload');
            if po_compilerproc in procdef.procoptions then
              PrintOption('compilerproc');
            if po_assembler in procdef.procoptions then
              PrintOption('assembler');
            if po_nostackframe in procdef.procoptions then
              PrintOption('nostackframe');
            if po_inline in procdef.procoptions then
              PrintOption('inline');
            if po_noreturn in procdef.procoptions then
              PrintOption('noreturn');
            if po_noinline in procdef.procoptions then
              PrintOption('noinline');
          end;

          if Assigned(Code) then
            begin
              if FirstHalf then
                WriteLn(T, PrintNodeIndention, '<code>')
              else
                begin
                  WriteLn(T); { Line for spacing }
                  WriteLn(T, PrintNodeIndention, '<firstpass>');
                end;

              PrintNodeIndent;
              XMLPrintNode(T, Code);
              PrintNodeUnindent;

              if FirstHalf then
                WriteLn(T, PrintNodeIndention, '</code>')
              else
                WriteLn(T, PrintNodeIndention, '</firstpass>');
            end
          else { Code=Nil }
            begin
              { Don't print anything for second half - if there's no code, there's no firstpass }
              if FirstHalf then
                WriteLn(T, PrintNodeIndention, '<code />');
            end;

        { Print footer only for second half }
        if (not FirstHalf) or separate then
          begin
            PrintNodeUnindent;
            WriteLn(T, PrintNodeIndention, '</subroutine>');
            WriteLn(T); { Line for spacing }
          end;

        Close(T);
      end;
{$endif DEBUG_NODE_XML}

    procedure tcgprocinfo.generate_code_tree;
      var
        hpi : tcgprocinfo;
      begin
        { generate code for this procedure }
        generate_code;
        { process nested procedures }
        hpi:=tcgprocinfo(get_first_nestedproc);
        while assigned(hpi) do
          begin
            if not (df_generic in hpi.procdef.defoptions) then
              hpi.generate_code_tree;
            hpi:=tcgprocinfo(hpi.next);
          end;
        resetprocdef;
      end;


    procedure tcgprocinfo.generate_code_exceptfilters;
      var
        hpi : tcgprocinfo;
      begin
        { On AArch64-Win64, exceptfilter code generation is deferred (after the
          parent's prolog has been emitted). Record the parent's current lasttemp
          so that adjust_exceptfilter_ref can distinguish parent temps (which must
          be converted to FP-relative) from handler temps (which stay SP-relative
          in the handler's own stack frame). Also discard the TG free list to
          prevent handler temps from reusing freed parent param/local slots,
          which would cause collisions with still-live parent data. }
        if target_info.system=system_aarch64_win64 then
          begin
            exceptfilter_parent_tempend:=tg.lasttemp;
            tg.discard_freelist;
          end;
        hpi:=tcgprocinfo(get_first_nestedproc);
        while assigned(hpi) do
          begin
            if (hpi.procdef.proctypeoption=potype_exceptfilter) and
               assigned(hpi.code) then
              begin
                hpi.apply_tempflags;
                generate_exceptfilter(hpi);
                hpi.reset_tempflags;
              end;
            hpi:=tcgprocinfo(hpi.next);
          end;
      end;

    { For SEH, the code from 'finally' blocks must be put into a separate procedures,
      which can be called by OS during stack unwind. This resembles nested procedures,
      but finalizer procedures do not have their own local variables and work directly
      with the stack frame of parent. In particular, the tempgen must be shared, so
      1) finalizer procedure is able to finalize temps of the parent,
      2) if the finalizer procedure is complex enough to need its own temps, they are
         allocated in stack frame of parent, so second-level finalizer procedures are
         not needed.

      Due to requirement of shared tempgen we cannot process finalizer as a regular nested
      procedure (after the parent) and have to do it inline.
      This is called by platform-specific tryfinallynodes during pass2.
      Here we put away the codegen (which carries the register allocator state), process
      the 'nested' procedure, then restore previous cg and continue processing the parent
      procedure. generate_code() will create another cg, but not another tempgen because
      setup_tempgen() is not called for potype_exceptfilter procedures. }

    procedure tcgprocinfo.generate_exceptfilter(nestedpi: tcgprocinfo);
      var
        saved_cg: tcg;
        saved_hlcg: thlcgobj;
{$ifdef cpu64bitalu}
        saved_cg128 : tcg128;
{$else cpu64bitalu}
        saved_cg64 : tcg64;
{$endif cpu64bitalu}
      begin
        if nestedpi.procdef.proctypeoption<>potype_exceptfilter then
          InternalError(201201141);
        { flush code generated this far }
        aktproccode.concatlist(current_asmdata.CurrAsmList);
        { save the codegen }
        saved_cg:=cg;
        saved_hlcg:=hlcg;
        cg:=nil;
        hlcg:=nil;
{$ifdef cpu64bitalu}
        saved_cg128:=cg128;
        cg128:=nil;
{$else cpu64bitalu}
        saved_cg64:=cg64;
        cg64:=nil;
{$endif cpu64bitalu}
        nestedpi.generate_code;
        { prevents generating code the second time when processing nested procedures }
        nestedpi.resetprocdef;
        cg:=saved_cg;
        hlcg:=saved_hlcg;
{$ifdef cpu64bitalu}
        cg128:=saved_cg128;
{$else cpu64bitalu}
        cg64:=saved_cg64;
{$endif cpu64bitalu}
        add_reg_instruction_hook:=@cg.add_reg_instruction;
      end;


    procedure tcgprocinfo.generate_exit_label(list: tasmlist);
      begin
        hlcg.a_label(list,CurrExitLabel);
      end;


    procedure tcgprocinfo.convert_captured_syms;
      var
        hpi : tcgprocinfo;
        old_current_procinfo : tprocinfo;
      begin
        { do the conversion only if there haven't been any errors so far }
        if ErrorCount<>0 then
          exit;
        old_current_procinfo:=current_procinfo;
        current_procinfo:=self;
        { process nested procedures }
        hpi:=tcgprocinfo(get_first_nestedproc);
        while assigned(hpi) do
          begin
            hpi.convert_captured_syms;
            hpi:=tcgprocinfo(hpi.next);
          end;
        { convert the captured symbols for this routine }
        if assigned(code) then
          procdefutil.convert_captured_syms(procdef,code);
        current_procinfo:=old_current_procinfo;
      end;


     procedure TCGProcinfo.CreateInlineInfo;
       begin
        new(procdef.inlininginfo);
        procdef.inlininginfo^.code:=code.getcopy;
        procdef.inlininginfo^.flags:=flags;
        { The blocknode needs to set an exit label }
        if procdef.inlininginfo^.code.nodetype=blockn then
          include(procdef.inlininginfo^.code.flags,nf_block_with_exit);
        procdef.has_inlininginfo:=true;
        export_local_ref_syms;
        export_local_ref_defs;
       end;

    procedure searchthreadvar(p: TObject; arg: pointer);
      var
        i : longint;
        pd : tprocdef;
      begin
        case tsym(p).typ of
          staticvarsym :
            begin
                  { local (procedure or unit) variables only need finalization
                    if they are used
                  }
              if (vo_is_thread_var in tstaticvarsym(p).varoptions) and
                 ((tstaticvarsym(p).refs>0) or
                  { global (unit) variables always need finalization, since
                    they may also be used in another unit
                  }
                  (tstaticvarsym(p).owner.symtabletype=globalsymtable)) and
                  (
                    (tstaticvarsym(p).varspez<>vs_const) or
                    (vo_force_finalize in tstaticvarsym(p).varoptions)
                  ) and
                 not(vo_is_funcret in tstaticvarsym(p).varoptions) and
                 not(vo_is_external in tstaticvarsym(p).varoptions) and
                 is_managed_type(tstaticvarsym(p).vardef) then
                include(current_procinfo.flags,pi_uses_threadvar);
            end;
          procsym :
            begin
              for i:=0 to tprocsym(p).ProcdefList.Count-1 do
                begin
                  pd:=tprocdef(tprocsym(p).ProcdefList[i]);
                  if assigned(pd.localst) and
                     (pd.procsym=tprocsym(p)) and
                     (pd.localst.symtabletype<>staticsymtable) then
                    pd.localst.SymList.ForEachCall(@searchthreadvar,arg);
                end;
            end;
          else
            ;
        end;
      end;


    function searchusercode(var n: tnode; arg: pointer): foreachnoderesult;
      begin
        if nf_usercode_entry in n.flags then
          begin
            pnode(arg)^:=n;
            result:=fen_norecurse_true
          end
        else
          result:=fen_false;
      end;


    function TCGProcinfo.GetUserCode : tnode;
      var
        n : tnode;
      begin
        n:=nil;
        foreachnodestatic(code,@searchusercode,@n);
        if not(assigned(n)) then
          internalerror(2013111004);
        result:=n;
      end;


    procedure tcgprocinfo.generate_code;

       procedure check_for_threadvars_in_initfinal;
         begin
           if current_procinfo.procdef.proctypeoption=potype_unitfinalize then
             begin
                { this is also used for initialization of variables in a
                  program which does not have a globalsymtable }
                if assigned(current_module.globalsymtable) then
                  TSymtable(current_module.globalsymtable).SymList.ForEachCall(@searchthreadvar,nil);
                TSymtable(current_module.localsymtable).SymList.ForEachCall(@searchthreadvar,nil);
             end;
         end;

       function heuristics_favors_autoinlining(code: tnode): boolean;
         var
           complexityAvail : integer;
         begin
           { rough approximation if we should auto inline:
             - if the tree is simple enough
             - if the tree is not too big
             A bigger tree which is simpler might be autoinlined otoh
             a smaller and complexer tree as well: so we use the sum of
             both measures here }

           { This is a shortcutted version of
             "result:=node_count(code)+node_complexity(code)<=25". }
           complexityAvail:=25-node_complexity(code);
           result:=(complexityAvail>0) and (node_count(code,complexityAvail+1)<=dword(complexityAvail));
         end;

      var
        old_current_procinfo : tprocinfo;
        oldmaxfpuregisters : longint;
        oldfilepos : tfileposinfo;
        old_current_structdef : tabstractrecorddef;
        oldswitches : tlocalswitches;
        templist : TAsmList;
        headertai : tai;
        blk_i : longint;

      procedure delete_marker(anode: tasmnode);
        var
          ai: tai;
        begin
          if assigned(anode) then
            begin
              ai:=anode.currenttai;
              if assigned(ai) then
                begin
                  aktproccode.remove(ai);
                  ai.free;
                  ai := nil;
                  anode.currenttai:=nil;
                end;
            end;
        end;

      begin
        { the initialization procedure can be empty, then we
          don't need to generate anything. When it was an empty
          procedure there would be at least a blocknode }
        if not assigned(code) then
          begin
{$ifdef DEBUG_NODE_XML}
            { Print out nodes as they appear after the first pass }
            XMLPrintProc(True);
            XMLPrintProc(False);
{$endif DEBUG_NODE_XML}
            exit;
          end;

        { We need valid code }
        if Errorcount<>0 then
          exit;

        { No code can be generated for generic template }
        if (df_generic in procdef.defoptions) then
          internalerror(200511152);

        { For regular procedures the RA and Tempgen shall not be available yet,
          but exception filters reuse Tempgen of parent }
        if assigned(tg)<>(procdef.proctypeoption=potype_exceptfilter) then
          internalerror(200309201);

        old_current_procinfo:=current_procinfo;
        oldfilepos:=current_filepos;
        old_current_structdef:=current_structdef;
        oldmaxfpuregisters:=current_settings.maxfpuregisters;

        current_procinfo:=self;
        current_filepos:=entrypos;
        current_structdef:=procdef.struct;

        { store start of user code, it must be a block node, it will be used later one to
          check variable lifeness }
        include(code.flags,nf_usercode_entry);

        { add wrapping code if necessary (initialization of typed constants on
          some platforms, initing of local variables and out parameters with
          trashing values, ...) }
        { init/final code must be wrapped later (after code for main proc body
          has been generated) }
        if not(current_procinfo.procdef.proctypeoption in [potype_unitinit,potype_unitfinalize]) then
          code:=cnodeutils.wrap_proc_body(procdef,code);

        { automatic inlining? }
        if (cs_opt_autoinline in current_settings.optimizerswitches) and
           not(po_noinline in procdef.procoptions) and
           { no inlining yet? }
           not(procdef.has_inlininginfo) and not(has_nestedprocs) and
            not(procdef.proctypeoption in [potype_proginit,potype_unitinit,potype_unitfinalize,potype_constructor,
                                           potype_destructor,potype_class_constructor,potype_class_destructor]) and
            ((procdef.procoptions*[po_exports,po_external,po_interrupt,po_virtualmethod,po_iocheck])=[]) and
            (not(procdef.proccalloption in [pocall_safecall])) and
            heuristics_favors_autoinlining(code) then
          begin
            { Can we inline this procedure? }
            if checknodeinlining(procdef) then
              begin
                Message1(cg_d_autoinlining,procdef.GetTypeName);
                include(procdef.procoptions,po_inline);
                CreateInlineInfo;
              end;
          end;

        { -OoDEVIRT: retain the body of a small VIRTUAL method as inlining info
          so that a call proven to reach exactly this override (devirtualized in
          another routine of this unit) can be expanded inline. A virtual method
          can never carry po_inline -- it is mutually exclusive with
          po_virtualmethod, so both the ordinary and the automatic inliner skip
          it -- hence the body is kept WITHOUT setting po_inline: normal virtual
          dispatch is unaffected, only tcallnode.devirt_prepare_inline consumes
          the retained body. Same soundness gate (checknodeinlining) and size
          heuristic as auto-inlining; excludes constructors/destructors (never
          devirtualized) and the same unsafe proc kinds. }
        if (cs_opt_devirt in current_settings.optimizerswitches) and
           (po_virtualmethod in procdef.procoptions) and
           not(po_noinline in procdef.procoptions) and
           not(procdef.has_inlininginfo) and not(has_nestedprocs) and
           not(procdef.proctypeoption in [potype_proginit,potype_unitinit,potype_unitfinalize,potype_constructor,
                                          potype_destructor,potype_class_constructor,potype_class_destructor]) and
           ((procdef.procoptions*[po_exports,po_external,po_interrupt,po_iocheck,po_assembler,po_abstractmethod])=[]) and
           (not(procdef.proccalloption in [pocall_safecall])) and
           heuristics_favors_autoinlining(code) then
          begin
            if checknodeinlining(procdef) then
              CreateInlineInfo;   { deliberately WITHOUT include(procoptions,po_inline) }
          end;

        { -OoIPACP cross-unit: retain the body of an IPACP-eligible routine that
          is reachable from another unit (an interface routine, or one already
          inline) as inlininginfo so its tree is streamed into this unit's PPU.
          A caller in a USED unit then recovers that tree and clones it,
          specialized on the constants it passes.  Retained WITHOUT po_inline
          (like the DEVIRT retention above): ordinary call/inlining behaviour is
          unchanged, only optipacp.make_crossunit_stash consumes the tree, and
          it independently re-verifies eligibility on the loaded copy.  Gated on
          the same size/eligibility screen as the intra-unit stash, so PPU bloat
          is bounded to routines that could actually be specialized. }
        if (cs_opt_ipacp in current_settings.optimizerswitches) and
           not(procdef.has_inlininginfo) and not(has_nestedprocs) and
           ipacp_crossunit_retain_candidate(procdef,code,flags,has_nestedprocs) then
          CreateInlineInfo;   { deliberately WITHOUT include(procoptions,po_inline) }

        templist:=TAsmList.create;

        { add parast/localst to symtablestack }
        add_to_symtablestack;

        { clear register count }
        procdef.localst.SymList.ForEachCall(@clearrefs,nil);
        if assigned(procdef.blocklocalsymtables) then
          for blk_i:=0 to procdef.blocklocalsymtables.count-1 do
            TSymtable(procdef.blocklocalsymtables[blk_i]).SymList.ForEachCall(@clearrefs,nil);
        procdef.parast.SymList.ForEachCall(@clearrefs,nil);

        { there's always a call to FPC_INITIALIZEUNITS/FPC_DO_EXIT in the main program }
        if (procdef.localst.symtablelevel=main_program_level) and
           (not current_module.is_unit) then
          begin
            include(flags,pi_do_call);
            { the main program never returns due to the do_exit call }
            if not(current_module.islibrary) and (procdef.proctypeoption=potype_proginit) then
              include(procdef.procoptions,po_noreturn);
          end;

        { set implicit_finally flag when there are locals/paras to be finalized }
        if not(po_assembler in current_procinfo.procdef.procoptions) then
          begin
            procdef.parast.SymList.ForEachCall(@check_finalize_paras,nil);
            procdef.localst.SymList.ForEachCall(@check_finalize_locals,nil);
            { also check block-scoped inline vars }
            if assigned(procdef.blocklocalsymtables) then
              for blk_i:=0 to procdef.blocklocalsymtables.count-1 do
                TSymtable(procdef.blocklocalsymtables[blk_i]).SymList.ForEachCall(@check_finalize_locals,nil);
          end;

{$ifdef SUPPORT_SAFECALL}
        { set implicit_finally flag for if procedure is safecall }
        if (tf_safecall_exceptions in target_info.flags) and
           (procdef.proccalloption=pocall_safecall) then
          include(flags, pi_needs_implicit_finally);
{$endif}
{$ifdef DEBUG_NODE_XML}
        { Print out nodes as they appear after the first pass }
        XMLPrintProc(True);
{$endif DEBUG_NODE_XML}

        { managed-type reference-count traffic elision (-OoREFELIDE): remove the
          redundant incref/decref pair of an ansistring local that merely
          borrows its value from a value-parameter or single-assignment local
          and is then only read. Must run HERE, before do_firstpass lowers the
          a := b assignment into an fpc_ansistr_assign call; the analysis is
          flow-insensitive (whole-routine occurrence counts + addr_taken flags)
          so it is sound under any control flow and across the implicit finally
          frame. Skipped for assembler routines (no analysable node tree). }
        if (cs_opt_refelide in current_settings.optimizerswitches) and
           not(po_assembler in current_procinfo.procdef.procoptions) then
          OptimizeRefElide(code);

        { escape-analysis stack allocation of a non-escaping local dynamic array
          with a single small constant-length SetLength (-OoSTACKALLOC): replace
          its heap buffer with a hidden stack record. Must run HERE, before
          do_firstpass lowers SetLength into an fpc_dynarr_setlength call -- the
          analysis matches the in_setlength_x inline node and the plain A[i] /
          Length loads. Disqualify routines that are recursive, use exceptions,
          or contain inline assembler (opaque storage / unproven control flow);
          the whole-routine escape walk is otherwise flow-insensitive and thus
          sound under any control flow. }
        if (cs_opt_stackalloc in current_settings.optimizerswitches) and
           ((flags*[pi_has_assembler_block,pi_is_assembler,pi_uses_exceptions])=[]) and
           not(pi_is_recursive in flags) and
           not(po_assembler in current_procinfo.procdef.procoptions) then
          OptimizeStackAlloc(code);

        { firstpass everything }
        flowcontrol:=[];
        do_firstpass(code);

{$if defined(i386) or defined(i8086)}
        if node_resources_fpu(code)>0 then
          include(flags,pi_uses_fpu);
{$endif i386 or i8086}

        { Print the node to tree.log }
        if paraprintnodetree <> 0 then
          printproc( 'after the firstpass');

        TransformNodeTree;

        { unit static/global symtables might contain threadvars which are not explicitly used but which might
          require a tls register, so check for such variables }
        check_for_threadvars_in_initfinal;

        { add implicit entry and exit code }
        add_entry_exit_code;

        { only do secondpass if there are no errors }
        if (ErrorCount<>0) then
          begin
{$ifdef DEBUG_NODE_XML}
            { Print out nodes as they appear after the first pass }
            XMLPrintProc(False);
{$endif DEBUG_NODE_XML}
          end
        else
          begin
            create_hlcodegen;

            setup_eh;

            if (procdef.proctypeoption<>potype_exceptfilter) then
              setup_tempgen;

            { Create register allocator, must come after framepointer is known }
            hlcg.init_register_allocators;

            generate_parameter_info;

            { allocate got register if needed }
            allocate_got_register(aktproccode);

            { allocate got register if needed }
            allocate_tls_register(aktproccode);

            { Allocate space in temp/registers for parast and localst }
            current_filepos:=entrypos;
            gen_alloc_symtable(aktproccode,procdef,procdef.parast);
            gen_alloc_symtable(aktproccode,procdef,procdef.localst);
            { Allocate space for block-scoped inline vars (m_inline_var) }
            if assigned(procdef.blocklocalsymtables) then
              for blk_i:=0 to procdef.blocklocalsymtables.count-1 do
                gen_alloc_symtable(aktproccode,procdef,TSymtable(procdef.blocklocalsymtables[blk_i]));

            { Store temp offset for information about 'real' temps }
            tempstart:=tg.lasttemp;

            { Generate code to load register parameters in temps and insert local
              copies for values parameters. This must be done before the code for the
              body is generated because the localloc is updated.
              Note: The generated code will be inserted after the code generation of
              the body is finished, because only then the position is known }
            current_filepos:=entrypos;

            hlcg.gen_load_para_value(templist);

            { caller paraloc info is also necessary in the stackframe_entry
              code of the ppc (and possibly other processors)               }
            procdef.init_paraloc_info(callerside);

            CalcExecutionWeights(code);

            { Print the node to tree.log }
            if paraprintnodetree <> 0 then
              printproc( 'right before code generation');

{$ifdef DEBUG_NODE_XML}
            { Print out nodes as they appear after the first pass }
            XMLPrintProc(False);
{$endif DEBUG_NODE_XML}

            { generate code for the node tree }
            do_secondpass(code);
            aktproccode.concatlist(current_asmdata.CurrAsmList);

            { The position of the loadpara_asmnode is now known }
            aktproccode.insertlistafter(loadpara_asmnode.currenttai,templist);

            oldswitches:=current_settings.localswitches;

            { first generate entry and initialize code with the correct
              position and switches }
            current_filepos:=entrypos;
            current_settings.localswitches:=entryswitches;

            cg.set_regalloc_live_range_direction(rad_backwards);

            hlcg.gen_entry_code(templist);
            aktproccode.insertlistafter(entry_asmnode.currenttai,templist);
            hlcg.gen_initialize_code(templist);
            aktproccode.insertlistafter(init_asmnode.currenttai,templist);

            { now generate finalize and exit code with the correct position
              and switches }
            current_filepos:=exitpos;
            current_settings.localswitches:=exitswitches;

            cg.set_regalloc_live_range_direction(rad_forward);

            if assigned(finalize_procinfo) then
              begin
                if target_info.system in [system_aarch64_win64] then
                  tcgprocinfo(finalize_procinfo).store_tempflags
                else
                  generate_exceptfilter(tcgprocinfo(finalize_procinfo));
              end
            else if not temps_finalized then
              begin
                hlcg.gen_finalize_code(templist);
                { the finalcode must be concatenated if there was no position available,
                  using insertlistafter will result in an insert at the start
                  when currentai=nil }
                aktproccode.concatlist(templist);
              end;
            { insert exit label at the correct position }
            generate_exit_label(templist);
            if assigned(exitlabel_asmnode.currenttai) then
              aktproccode.insertlistafter(exitlabel_asmnode.currenttai,templist)
            else
              aktproccode.concatlist(templist);
            { exit code }
            hlcg.gen_exit_code(templist);
            aktproccode.concatlist(templist);

            { reset switches }
            current_settings.localswitches:=oldswitches;

            { generate symbol and save end of header position }
            current_filepos:=entrypos;
            hlcg.gen_proc_symbol(templist);
            headertai:=tai(templist.last);
            { insert symbol }
            aktproccode.insertlist(templist);

            { Free space in temp/registers for parast and localst, must be
              done after gen_entry_code }
            current_filepos:=exitpos;

            { make sure the got/pic register doesn't get freed in the }
            { middle of a loop                                        }
            if (tf_pic_uses_got in target_info.flags) and
              (pi_needs_got in flags) and
              (got<>NR_NO) then
              cg.a_reg_sync(aktproccode,got);

            if (pi_needs_tls in flags) and
              (tlsoffset<>NR_NO) then
              cg.a_reg_sync(aktproccode,tlsoffset);

            if assigned(procdef.blocklocalsymtables) then
              for blk_i:=procdef.blocklocalsymtables.count-1 downto 0 do
                gen_free_symtable(aktproccode,TSymtable(procdef.blocklocalsymtables[blk_i]));
            gen_free_symtable(aktproccode,procdef.localst);
            gen_free_symtable(aktproccode,procdef.parast);

            { add code that will load the return value, this is not done
              for assembler routines when they didn't reference the result
              variable }
            hlcg.gen_load_return_value(templist);
            aktproccode.concatlist(templist);

            { Already reserve all registers for stack checking code and
              generate the call to the helper function }
            if not(tf_no_generic_stackcheck in target_info.flags) and
               (cs_check_stack in entryswitches) and
               not(po_assembler in procdef.procoptions) and
               (procdef.proctypeoption<>potype_proginit) then
              begin
                current_filepos:=entrypos;
                hlcg.gen_stack_check_call(templist);
                aktproccode.insertlistafter(stackcheck_asmnode.currenttai,templist);
              end;

            { this code (got loading) comes before everything which has }
            { already been generated, so reset the info about already   }
            { backwards extended registers (so their live range can be  }
            { extended backwards even further if needed)                }
            { This code must be                                         }
            {  a) generated after do_secondpass has been called         }
            {     (because pi_needs_got may be set there)               }
            {  b) generated before register allocation, because the     }
            {     got/pic register can be a virtual one                 }
            {  c) inserted before the entry code, because the entry     }
            {     code may need global symbols such as init rtti        }
            {  d) inserted after the stackframe allocation, because     }
            {     this register may have to be spilled                  }
            cg.set_regalloc_live_range_direction(rad_backwards_reinit);
            current_filepos:=entrypos;
            { load got if necessary }
            cg.g_maybe_got_init(templist);
            aktproccode.insertlistafter(headertai,templist);

            { init tls if needed }
            cg.g_maybe_tls_init(templist);
            aktproccode.insertlistafter(stackcheck_asmnode.currenttai,templist);

            { re-enable if more code at the end is ever generated here
            cg.set_regalloc_live_range_direction(rad_forward);
            }


{$ifndef NoOpt}
{$ifndef i386}
            if (cs_opt_scheduler in current_settings.optimizerswitches) and
              { do not optimize pure assembler procedures }
              not(pi_is_assembler in flags) then
              preregallocschedule(aktproccode);
{$endif i386}
{$endif NoOpt}

            { The procedure body is finished, we can now
              allocate the registers }
            cg.do_register_allocation(aktproccode,headertai);

            { translate imag. register to their real counter parts
              this is necessary for debuginfo and verbose assembler output
              when SSA will be implemented, this will be more complicated because we've to
            maintain location lists }
            procdef.parast.SymList.ForEachCall(@translate_registers,templist);
            procdef.localst.SymList.ForEachCall(@translate_registers,templist);
            if assigned(procdef.blocklocalsymtables) then
              for blk_i:=0 to procdef.blocklocalsymtables.count-1 do
                TSymtable(procdef.blocklocalsymtables[blk_i]).SymList.ForEachCall(@translate_registers,templist);
            if (tf_pic_uses_got in target_info.flags) and (pi_needs_got in flags) and
               not(cs_no_regalloc in current_settings.globalswitches) and
               (got<>NR_NO) then
              cg.translate_register(got);

            { Add save and restore of used registers }
            current_filepos:=entrypos;
            gen_save_used_regs(templist);
            { Remember the last instruction of register saving block
              (may be =nil for e.g. assembler procedures) }
            endprologue_ai:=templist.last;
            aktproccode.insertlistafter(headertai,templist);
            current_filepos:=exitpos;
            gen_restore_used_regs(aktproccode);
            { We know the size of the stack, now we can generate the
              parameter that is passed to the stack checking code }
            if not(tf_no_generic_stackcheck in target_info.flags) and
               (cs_check_stack in entryswitches) and
               not(po_assembler in procdef.procoptions) and
               (procdef.proctypeoption<>potype_proginit) then
              begin
                current_filepos:=entrypos;
                hlcg.gen_stack_check_size_para(templist);
                aktproccode.insertlistafter(stackcheck_asmnode.currenttai,templist)
              end;

            current_procinfo.set_eh_info;

            { Add entry code (stack allocation) after header }
            current_filepos:=entrypos;
            gen_proc_entry_code(templist);
            aktproccode.insertlistafter(headertai,templist);
{$ifdef SUPPORT_SAFECALL}
            { Set return value of safecall procedure if implicit try/finally blocks are disabled }
            if not (cs_implicit_exceptions in current_settings.moduleswitches) and
               (tf_safecall_exceptions in target_info.flags) and
               (procdef.proccalloption=pocall_safecall) then
              cg.a_load_const_reg(aktproccode,OS_ADDR,0,NR_FUNCTION_RETURN_REG);
{$endif}
            { Add exit code at the end }
            current_filepos:=exitpos;
            gen_proc_exit_code(templist);
            aktproccode.concatlist(templist);

            { check if the implicit finally has been generated. The flag
              should already be set in pass1 }
            if (cs_implicit_exceptions in current_settings.moduleswitches) and
               not(procdef.proctypeoption in [potype_unitfinalize,potype_unitinit]) and
               (pi_needs_implicit_finally in flags) and
               not(pi_has_implicit_finally in flags) and
               not(target_info.system in systems_garbage_collected_managed_types) then
             internalerror(200405231);

             { sanity check }
             if not(assigned(current_procinfo.procdef.personality)) and
                (tf_use_psabieh in target_info.flags) and
                ((pi_uses_exceptions in flags) or
                 ((cs_implicit_exceptions in current_settings.moduleswitches) and
                  (pi_needs_implicit_finally in flags))) then
               Internalerror(2019021005);

            { Position markers are only used to insert additional code after the secondpass
              and before this point. They are of no use in optimizer. Instead of checking and
              ignoring all over the optimizer, just remove them here. }
            delete_marker(entry_asmnode);
            delete_marker(loadpara_asmnode);
            delete_marker(exitlabel_asmnode);
            delete_marker(stackcheck_asmnode);
            delete_marker(init_asmnode);

{$ifndef NoOpt}
            if not(cs_no_regalloc in current_settings.globalswitches) then
              begin
                if (cs_opt_level1 in current_settings.optimizerswitches) and
                   { do not optimize pure assembler procedures }
                   not(pi_is_assembler in flags)  then
                  optimize(aktproccode);
{$ifndef i386}
                { schedule after assembler optimization, it could have brought up
                  new schedule possibilities }
                if (cs_opt_scheduler in current_settings.optimizerswitches) and
                  { do not optimize pure assembler procedures }
                  not(pi_is_assembler in flags)  then
                  preregallocschedule(aktproccode);
{$endif i386}
              end;
{$endif NoOpt}

            { Perform target-specific processing if necessary }
            postprocess_code;

            { Add end symbol and debug info }
            { this must be done after the pcrelativedata is appended else the distance calculation of
              insertpcrelativedata will be wrong, further the pc indirect data is part of the procedure
              so it should be inserted before the end symbol (FK)
            }
            current_filepos:=exitpos;
            hlcg.gen_proc_symbol_end(templist);
            aktproccode.concatlist(templist);

            { insert line debuginfo }
            if (cs_debuginfo in current_settings.moduleswitches) or
               (cs_use_lineinfo in current_settings.globalswitches) then
             begin
               { We only do this after the code generated because
                 otherwise for-loop counters moved to the struct cause
                 errors. And doing it before optimisation passes have run
                 causes problems when they manually look up symbols
                 like result and self (nutils.load_self_node etc). Still
                 do it nevertheless to to assist debug info generation
                 (hide original symbols, add absolutevarsyms that redirect
                  to their new locations in the parentfpstruct) }
              if assigned(current_procinfo.procdef.parentfpstruct) then
                redirect_parentfpstruct_local_syms(current_procinfo.procdef);
              current_debuginfo.insertlineinfo(aktproccode);
             end;

            finish_eh;

{$ifdef x86_64}
            { -OoIPARA: now that the body is register-allocated and all entry/exit
              code has been emitted, snapshot the routine's real volatile-register
              clobber set for direct callers generated later in this unit. The
              register allocators are still alive (done_register_allocators below). }
            if (cs_opt_ipara in current_settings.optimizerswitches) and
               not(cs_no_regalloc in current_settings.globalswitches) then
              RecordProcClobbers(current_procinfo.procdef,current_procinfo.flags);
{$endif x86_64}

            { -OoICF: associate this routine's mangled name with its procdef so
              the identical code folding pass (run later, at create_objectfile)
              can consult the procdef for its address-taken/visibility status and
              store its cross-unit canonical digest. }
            if cs_opt_icf in current_settings.optimizerswitches then
              ICFRegisterRoutine(current_procinfo.procdef.mangledname,current_procinfo.procdef);

            hlcg.record_generated_code_for_procdef(current_procinfo.procdef,aktproccode,aktlocaldata);

            { now generate code for any exception filters (they need the tempgen) }
            generate_code_exceptfilters;

            { only now we can remove the temps }
            if (procdef.proctypeoption<>potype_exceptfilter) then
              begin
                tg.resettempgen;
                tg.free;
                tg:=nil;
              end;
            { stop tempgen and ra }
            hlcg.done_register_allocators;
            destroy_hlcodegen;
          end;

        dfabuilder.free;
        dfabuilder := nil;

        { restore symtablestack }
        remove_from_symtablestack;

        { restore }
        templist.free;
        templist := nil;
        current_settings.maxfpuregisters:=oldmaxfpuregisters;
        current_filepos:=oldfilepos;
        current_structdef:=old_current_structdef;
        current_procinfo:=old_current_procinfo;
      end;


    procedure tcgprocinfo.set_code(p: tnode);
      begin
        code:=p;
      end;


    procedure tcgprocinfo.add_to_symtablestack;
      begin
        { insert symtables for the class, but only if it is no nested function }
        if assigned(procdef.struct) and
           not(assigned(parent) and
               assigned(parent.procdef) and
               assigned(parent.procdef.struct)) then
          push_nested_hierarchy(procdef.struct);

        { insert parasymtable in symtablestack when parsing
          a function }
        if procdef.parast.symtablelevel>=normal_function_level then
          symtablestack.push(procdef.parast);

        { insert localsymtable, except for the main procedure
          (in that case the localst is the unit's static symtable,
           which is already on the stack) }
        if procdef.localst.symtablelevel>=normal_function_level then
          symtablestack.push(procdef.localst);
      end;


    procedure tcgprocinfo.remove_from_symtablestack;
      begin
        { remove localsymtable }
        if procdef.localst.symtablelevel>=normal_function_level then
          symtablestack.pop(procdef.localst);

        { remove parasymtable }
        if procdef.parast.symtablelevel>=normal_function_level then
          symtablestack.pop(procdef.parast);

        { remove symtables for the class, but only if it is no nested function }
        if assigned(procdef.struct) and
           not(assigned(parent) and
               assigned(parent.procdef) and
               assigned(parent.procdef.struct)) then
          pop_nested_hierarchy(procdef.struct);
      end;


    procedure tcgprocinfo.resetprocdef;
      begin
         { remove code tree, if not inline procedure }
         if assigned(code) then
          begin
            { the inline procedure has already got a copy of the tree
              stored in procdef.inlininginfo }
            code.free;
            code:=nil;
          end;
       end;


    { Scans pd.parast for hidden params whose names start with '$dtup_'
      (generated by parse_parameter_dec for destructuring syntax) and
      creates corresponding local variables in pd.localst so the user
      names are available inside the procedure body. }
    procedure setup_tuple_destructure_locals(pd:tprocdef);
      var
        i,j,fcount : longint;
        sym : tsym;
        pvs : tparavarsym;
        pname : string;
        uname : string;
        recdef : trecorddef;
        fsym : tsym;
        lvs : tlocalvarsym;
        pos : longint;
      begin
        for i:=0 to pd.parast.symlist.count-1 do
          begin
            sym:=tsym(pd.parast.symlist[i]);
            if (sym.typ<>paravarsym) or
               (copy(sym.realname,1,5)<>'$dtup') then
              continue;
            pvs:=tparavarsym(sym);
            if pvs.vardef.typ<>recorddef then
              continue;
            recdef:=trecorddef(pvs.vardef);
            pname:=pvs.realname;
            { decode names: $dtup$name1$name2$... }
            fcount:=0;
            pos:=7; { skip '$dtup$' (6 chars) }
            for j:=0 to recdef.symtable.symlist.count-1 do
              begin
                fsym:=tsym(recdef.symtable.symlist[j]);
                if fsym.typ<>fieldvarsym then
                  continue;
                { extract next name from encoded param name }
                uname:='';
                while (pos<=length(pname)) and (pname[pos]<>'$') do
                  begin
                    uname:=uname+pname[pos];
                    inc(pos);
                  end;
                inc(pos); { skip separator $ }
                if (uname='') or (uname='_') then
                  continue;
                lvs:=clocalvarsym.create(uname,vs_value,tfieldvarsym(fsym).vardef,[]);
                lvs.register_sym;
                pd.localst.insertsym(lvs);
                lvs.varstate:=vs_initialised;
                inc(fcount);
              end;
          end;
      end;


    { prepend `localvar := Default(typeof(localvar))` for every tlocalvarsym
      in pd.localst. File types (and compounds containing them) are skipped:
      Default() rejects them and the RTL entry code already initialises file
      variables to their proper non-zero closed state. }
    procedure inject_zeroinit_locals(pd:tprocdef;var code:tnode);
      var
        i : longint;
        sym : tsym;
        lvs : tlocalvarsym;
        blk : tblocknode;
        laststmt : tstatementnode;
        had_any : boolean;
        zeronode : tnode;
      begin
        had_any:=false;
        blk:=nil;
        laststmt:=nil;
        for i:=0 to pd.localst.symlist.count-1 do
          begin
            sym:=tsym(pd.localst.symlist[i]);
            if sym.typ<>localvarsym then
              continue;
            lvs:=tlocalvarsym(sym);
            if (lvs.vardef.typ=filedef) or
               ((lvs.vardef.typ in [arraydef,recorddef,objectdef]) and
                not is_valid_for_default(lvs.vardef)) then
              continue;
            if not had_any then
              begin
                blk:=internalstatements(laststmt);
                had_any:=true;
              end;
            zeronode:=cinlinenode.create(in_default_x,false,
              ctypenode.create(lvs.vardef));
            addstatement(laststmt,
              cassignmentnode.create(
                cloadnode.create(lvs,lvs.owner),
                zeronode));
          end;
        if had_any and assigned(code) then
          begin
            addstatement(laststmt,code);
            code:=blk;
          end
        else if had_any then
          code:=blk;
      end;


    { For each hidden '$dtup_*' param, prepends assignments from the
      param's fields to the corresponding local vars. }
    procedure inject_tuple_destructure_assigns(pd:tprocdef;var code:tnode);
      var
        i,j : longint;
        sym : tsym;
        pvs : tparavarsym;
        pname : string;
        uname : string;
        recdef : trecorddef;
        fsym : tsym;
        lsym : tsym;
        lookst : tsymtable;
        pos : longint;
        blk : tblocknode;
        laststmt : tstatementnode;
        had_any : boolean;
      begin
        had_any:=false;
        blk:=nil;
        laststmt:=nil;
        for i:=0 to pd.parast.symlist.count-1 do
          begin
            sym:=tsym(pd.parast.symlist[i]);
            if (sym.typ<>paravarsym) or
               (copy(sym.realname,1,5)<>'$dtup') then
              continue;
            pvs:=tparavarsym(sym);
            if pvs.vardef.typ<>recorddef then
              continue;
            recdef:=trecorddef(pvs.vardef);
            pname:=pvs.realname;
            pos:=7;
            for j:=0 to recdef.symtable.symlist.count-1 do
              begin
                fsym:=tsym(recdef.symtable.symlist[j]);
                if fsym.typ<>fieldvarsym then
                  continue;
                uname:='';
                while (pos<=length(pname)) and (pname[pos]<>'$') do
                  begin
                    uname:=uname+pname[pos];
                    inc(pos);
                  end;
                inc(pos);
                if (uname='') or (uname='_') then
                  continue;
                if not searchsym(upper(uname),lsym,lookst) then
                  continue;
                if not had_any then
                  begin
                    blk:=internalstatements(laststmt);
                    had_any:=true;
                  end;
                addstatement(laststmt,
                  cassignmentnode.create(
                    cloadnode.create(lsym,lsym.owner),
                    csubscriptnode.create(fsym,
                      cloadnode.create(pvs,pvs.owner))));
              end;
          end;
        if had_any and assigned(code) then
          begin
            addstatement(laststmt,code);
            code:=blk;
          end;
      end;


    procedure tcgprocinfo.parse_body;
      var
         old_current_procinfo : tprocinfo;
         old_block_type : tblock_type;
         st : TSymtable;
         old_current_structdef: tabstractrecorddef;
         old_current_genericdef,
         old_current_specializedef: tstoreddef;
         parentfpinitblock: tnode;
         old_parse_generic: boolean;
         recordtokens : boolean;
      begin
         old_current_procinfo:=current_procinfo;
         old_block_type:=block_type;
         old_current_structdef:=current_structdef;
         old_current_genericdef:=current_genericdef;
         old_current_specializedef:=current_specializedef;
         old_parse_generic:=parse_generic;

         current_procinfo:=self;
         current_structdef:=procdef.struct;


        { check if the definitions of certain types are available which might not be available in older rtls and
          which are assigned "on the fly" in types_dec }
{$if not defined(jvm) and not defined(wasm)}
        if not assigned(rec_exceptaddr) then
          Message1(cg_f_internal_type_not_found,'TEXCEPTADDR');
        if not assigned(rec_tguid) then
          Message1(cg_f_internal_type_not_found,'TGUID');
        if not assigned(rec_jmp_buf) then
          Message1(cg_f_internal_type_not_found,'JMP_BUF');
{$endif}

         { if the procdef is truly a generic (thus takes parameters itself) then
           /that/ is our genericdef, not the - potentially - generic struct }
         if procdef.is_generic then
           begin
             current_genericdef:=procdef;
             parse_generic:=true;
           end
         else if assigned(current_structdef) and (df_generic in current_structdef.defoptions) then
           begin
             current_genericdef:=current_structdef;
             parse_generic:=true;
           end;
         if assigned(current_structdef) and (df_specialization in current_structdef.defoptions) then
           current_specializedef:=current_structdef;

         { calculate the lexical level }
         if procdef.parast.symtablelevel>maxnesting then
           Message(parser_e_too_much_lexlevel);
         block_type:=bt_body;

    {$ifdef state_tracking}
{    aktstate:=Tstate_storage.create;}
    {$endif state_tracking}

         { allocate the symbol for this procedure }
         alloc_proc_symbol(procdef);

         { add parast/localst to symtablestack }
         add_to_symtablestack;

         { create local variables for destructured tuple parameters }
         setup_tuple_destructure_locals(procdef);

         { save entry info }
         entrypos:=current_filepos;
         entryswitches:=current_settings.localswitches;

         recordtokens:=procdef.is_generic or
                       (
                         assigned(procdef.struct) and
                         (df_generic in procdef.struct.defoptions) and
                         assigned(procdef.owner) and
                         (procdef.owner.defowner=procdef.struct)
                       );

         if recordtokens then
           begin
             { start token recorder for generic template }
             procdef.initgeneric;
             current_scanner.startrecordtokens(procdef.generictokenbuf);
           end;

         { parse the code ... }
         code:=block(current_module.islibrary);

         { inject destructured tuple param assignments at start of body }
         inject_tuple_destructure_assigns(procdef,code);

         { inject zero-assignments at start of body for `zeroinit` routines }
         if pio_zeroinit in procdef.implprocoptions then
           inject_zeroinit_locals(procdef,code);

         postprocess_capturer(self);

         if recordtokens then
           begin
             { stop token recorder for generic template }
             current_scanner.stoprecordtokens;

             { Give an error for accesses in the static symtable that aren't visible
               outside the current unit }
             st:=procdef.owner;
             while (st.symtabletype in [ObjectSymtable,recordsymtable]) do
               st:=st.defowner.owner;
             if (pi_uses_static_symtable in flags) and
                (st.symtabletype<>staticsymtable) then
               Message(parser_e_global_generic_references_static);
           end;

         { save exit info }
         exitswitches:=current_settings.localswitches;
         exitpos:=last_endtoken_filepos;

         { the procedure is now defined }
         procdef.forwarddef:=false;
         procdef.is_implemented:=true;

         if assigned(code) then
           begin
             { get a better entry point }
             entrypos:=code.fileinfo;

             { Finish type checking pass }
             do_typecheckpass(code);

             { rewrite any `async`/`await` into the future-impl factory call and
               the `__Await` method call (no-op without them) }
             lower_async(self);

             if assigned(procdef.parentfpinitblock) then
               begin
                 if assigned(tblocknode(procdef.parentfpinitblock).left) then
                   begin
                     parentfpinitblock:=procdef.parentfpinitblock;
                     do_typecheckpass(parentfpinitblock);
                     procdef.parentfpinitblock:=parentfpinitblock;
                   end
               end;

           end;

         { Check for unused labels, forwards, symbols for procedures. Static
           symtable is checked in pmodules.
           The check must be done after the typecheckpass }
         if (Errorcount=0) and
            (tstoredsymtable(procdef.localst).symtabletype<>staticsymtable) then
           begin
             { check if forwards are resolved }
             tstoredsymtable(procdef.localst).check_forwards;
             { check if all labels are used }
             tstoredsymtable(procdef.localst).checklabels;
             { check for unused symbols, but only if there is no asm block }
             if not(pi_has_assembler_block in flags) then
               begin
                 tstoredsymtable(procdef.localst).allsymbolsused;
                 tstoredsymtable(procdef.parast).allsymbolsused;
               end;
           end;

         if (po_inline in procdef.procoptions) and
           { Can we inline this procedure? }
           checknodeinlining(procdef) then
           CreateInlineInfo;

         { Print the node to tree.log }
         if paraprintnodetree <> 0 then
           printproc( 'after parsing');

{$ifdef DEBUG_NODE_XML}
         { Methods of generic classes don't get any code generated, so output
           the node tree here }
         if (df_generic in procdef.defoptions) then
           XMLPrintProc(True);
{$endif DEBUG_NODE_XML}

         { ... remove symbol tables }
         remove_from_symtablestack;

    {$ifdef state_tracking}
{    aktstate.destroy;}
    {$endif state_tracking}

         current_structdef:=old_current_structdef;
         current_genericdef:=old_current_genericdef;
         current_specializedef:=old_current_specializedef;
         current_procinfo:=old_current_procinfo;
         parse_generic:=old_parse_generic;

         { Restore old state }
         block_type:=old_block_type;
      end;


{****************************************************************************
                        PROCEDURE/FUNCTION PARSING
****************************************************************************}


    procedure check_init_paras(p:TObject;arg:pointer);
      begin
        if tsym(p).typ<>paravarsym then
         exit;
        with tparavarsym(p) do
          if (is_managed_type(vardef) and
             (varspez in [vs_value,vs_out])) or
             (is_shortstring(vardef) and
             (varspez=vs_value)) then
            include(current_procinfo.flags,pi_do_call);
      end;


    { -OoPARTIALINLINE: compile a freshly-synthesised inline header procdef
      (built by optpartialinline.partialinline_make_header) whose node tree is
      HEADERCODE. Mirrors the tail of parse_body + read_proc_body for a body we
      already have as a node tree instead of parsing it from source. }
    procedure compile_partial_inline_header(headerpd:tprocdef;headercode:tnode);
      var
        oldpi : tprocinfo;
        oldmoduleprocinfo : tprocinfo;
        oldstructdef : tabstractrecorddef;
        oldblock : tblock_type;
        pi : tcgprocinfo;
      begin
        if not assigned(headerpd) or not assigned(headercode) then
          exit;
        oldpi:=current_procinfo;
        oldmoduleprocinfo:=tprocinfo(current_module.procinfo);
        oldstructdef:=current_structdef;
        oldblock:=block_type;

        pi:=tcgprocinfo(cprocinfo.create(nil));
        current_module.procinfo:=pi;
        pi.procdef:=headerpd;
        current_procinfo:=pi;
        current_structdef:=nil;
        block_type:=bt_body;

        headerpd.aliasnames.insert(headerpd.mangledname);
        alloc_proc_symbol(headerpd);

        pi.add_to_symtablestack;

        pi.entrypos:=headercode.fileinfo;
        pi.entryswitches:=current_settings.localswitches;
        pi.exitpos:=headercode.fileinfo;
        pi.exitswitches:=current_settings.localswitches;

        pi.code:=headercode;
        do_typecheckpass(pi.code);

        { store the inline copy before the tree is lowered by generate_code }
        if (po_inline in headerpd.procoptions) and checknodeinlining(headerpd) then
          pi.CreateInlineInfo;

        pi.remove_from_symtablestack;

        current_structdef:=oldstructdef;
        block_type:=oldblock;

        if Errorcount=0 then
          pi.generate_code_tree;

        freeandnil(pi);
        current_module.procinfo:=oldmoduleprocinfo;
        current_procinfo:=oldpi;
      end;


    { uninitialized-variable/result diagnostics silenced while a specialized
      IPACP clone (a copy of already-validated code) is code-generated }
    const
      ipacp_suppressed_msgs : array[0..8] of longint = (
        sym_w_uninitialized_local_variable,
        sym_w_uninitialized_variable,
        sym_h_uninitialized_local_variable,
        sym_h_uninitialized_variable,
        sym_w_function_result_uninitialized,
        sym_h_uninitialized_managed_local_variable,
        sym_h_uninitialized_managed_variable,
        sym_w_managed_function_result_uninitialized,
        { the constant fold that specializes the clone can make a case/if arm
          unreachable -- a true statement about the clone, noise on user lines }
        cg_w_unreachable_code);

    { -OoIPACP: compile a synthesised specialized clone procdef (built by
      optipacp.ipacp_process_calls) whose body is CLONECODE.  Like
      compile_partial_inline_header, but the clone is a normal out-of-line
      routine (no inline info is created), reentrantly code-generated at
      module-level symtablestack state after the caller's own
      generate_code_tree. }
    procedure compile_ipacp_clone(clonepd:tprocdef;clonecode:tnode);
      var
        oldpi : tprocinfo;
        oldmoduleprocinfo : tprocinfo;
        oldstructdef : tabstractrecorddef;
        oldblock : tblock_type;
        i : longint;
        savedmsgstate : array[0..high(ipacp_suppressed_msgs)] of tmsgstate;
        pi : tcgprocinfo;
      begin
        if not assigned(clonepd) or not assigned(clonecode) then
          exit;
        { The clone body is a copy of code already fully checked when the
          original routine compiled; the DFA run over the specialized copy can
          raise spurious uninitialized-variable/result warnings on the user's
          own source lines (constant folding can leave e.g. `Result:=a;
          Result:=Result+k` in a shape the partial life analysis flags even
          though the write dominates). Turn those specific messages off for the
          clone's compilation only; everything else (real errors) still fires. }
        for i:=0 to high(ipacp_suppressed_msgs) do
          begin
            savedmsgstate[i]:=GetMessageState(ipacp_suppressed_msgs[i]);
            SetMessageVerbosity(ipacp_suppressed_msgs[i],ms_off_global);
          end;
        oldpi:=current_procinfo;
        oldmoduleprocinfo:=tprocinfo(current_module.procinfo);
        oldstructdef:=current_structdef;
        oldblock:=block_type;

        pi:=tcgprocinfo(cprocinfo.create(nil));
        current_module.procinfo:=pi;
        pi.procdef:=clonepd;
        current_procinfo:=pi;
        current_structdef:=nil;
        block_type:=bt_body;

        clonepd.aliasnames.insert(clonepd.mangledname);
        alloc_proc_symbol(clonepd);

        pi.add_to_symtablestack;

        pi.entrypos:=clonecode.fileinfo;
        pi.entryswitches:=current_settings.localswitches;
        pi.exitpos:=clonecode.fileinfo;
        pi.exitswitches:=current_settings.localswitches;

        pi.code:=clonecode;
        do_typecheckpass(pi.code);

        pi.remove_from_symtablestack;

        current_structdef:=oldstructdef;
        block_type:=oldblock;

        if Errorcount=0 then
          pi.generate_code_tree;

        freeandnil(pi);
        current_module.procinfo:=oldmoduleprocinfo;
        current_procinfo:=oldpi;
        for i:=0 to high(ipacp_suppressed_msgs) do
          SetMessageVerbosity(ipacp_suppressed_msgs[i],savedmsgstate[i]);
      end;


    procedure ipacp_process_main_body(mainpi:tcgprocinfo);
      var
        ipacp_pending : TFPObjectList;
        ipacp_code    : tnode;
        i             : longint;
      begin
        if not (cs_opt_ipacp in current_settings.optimizerswitches) then
          exit;
        if not assigned(mainpi) or not assigned(mainpi.procdef) or
           not assigned(mainpi.code) then
          exit;
        { only the real program/library init body carries user code worth
          scanning; the synthetic stubs (mainstub/libmainstub/pkgstub) do not }
        if mainpi.procdef.proctypeoption<>potype_proginit then
          exit;
        ipacp_pending:=TFPObjectList.create(true);
        try
          ipacp_code:=mainpi.code;
          { mirror read_proc_body: the caller's symtables must be reachable while
            the retargeted call nodes are re-typechecked.  For the main proc the
            localst IS the module static symtable (already on the stack) and the
            (empty) parast sits below normal_function_level, so add/remove are
            effectively no-ops here -- but keep them for symmetry/safety. }
          mainpi.add_to_symtablestack;
          ipacp_process_calls(mainpi.procdef,ipacp_code,ipacp_pending);
          mainpi.remove_from_symtablestack;
          mainpi.code:=ipacp_code;
          { compile the synthesised clone bodies now (before the main body's own
            generate_code_tree): each is an independent out-of-line routine and
            only its symbol needs to exist for the retargeted call sites }
          for i:=0 to ipacp_pending.count-1 do
            compile_ipacp_clone(tipacpclone(ipacp_pending[i]).clonepd,
              tipacpclone(ipacp_pending[i]).clonecode);
        finally
          ipacp_pending.free;
        end;
      end;


    procedure read_proc_body(old_current_procinfo:tprocinfo;pd:tprocdef);
      {
        Parses the procedure directives, then parses the procedure body, then
        generates the code for it
      }

      var
        oldfailtokenmode : tmodeswitches;
        isnestedproc     : boolean;
        pi_dosplit       : boolean;
        pi_headerpd      : tprocdef;
        pi_headercode    : tnode;
        ipacp_pending    : TFPObjectList;
        ipacp_code       : tnode;
        ipacp_i          : longint;
      begin
        Message1(parser_d_procedure_start,pd.fullprocname(false));
        oldfailtokenmode:=[];
        pi_dosplit:=false;
        pi_headerpd:=nil;
        pi_headercode:=nil;
        ipacp_pending:=nil;

        { create a new procedure }
        current_procinfo:=cprocinfo.create(old_current_procinfo);
        current_module.procinfo:=current_procinfo;
        current_procinfo.procdef:=pd;
        isnestedproc:=(current_procinfo.procdef.parast.symtablelevel>normal_function_level);
        { an anonymous function is always considered as nested }
        if po_anonymous in pd.procoptions then
          begin
            current_procinfo.force_nested;
            isnestedproc:=true;
          end;

        { Insert mangledname }
        pd.aliasnames.insert(pd.mangledname);

        { Handle Export of this procedure }
        if (po_exports in pd.procoptions) and
           (target_info.system in [system_i386_os2,system_i386_emx]) then
          begin
            pd.aliasnames.insert(pd.procsym.realname);
            if cs_link_deffile in current_settings.globalswitches then
              deffile.AddExport(pd.mangledname);
          end;

        { Insert result variables in the localst }
        insert_funcret_local(pd);

        { check if there are para's which require initing -> set }
        { pi_do_call (if not yet set)                            }
        if not(pi_do_call in current_procinfo.flags) then
          pd.parast.SymList.ForEachCall(@check_init_paras,nil);

        { set _FAIL as keyword if constructor }
        if (pd.proctypeoption=potype_constructor) then
         begin
           oldfailtokenmode:=tokeninfo^[_FAIL].keyword;
           tokeninfo^[_FAIL].keyword:=alllanguagemodes;
         end;

        tcgprocinfo(current_procinfo).parse_body;

        { reset _FAIL as _SELF normal }
        if (pd.proctypeoption=potype_constructor) then
          tokeninfo^[_FAIL].keyword:=oldfailtokenmode;

        { -OoPARTIALINLINE: decide whether this routine is a partial-inline
          candidate and, if so, snapshot its leading guard now -- before
          generate_code_tree lowers or frees the node tree }
        if (cs_opt_partialinline in current_settings.optimizerswitches) and
           (not isnestedproc) and
           (not(df_generic in pd.defoptions)) then
          pi_dosplit:=partialinline_candidate(pd,
            tcgprocinfo(current_procinfo).code,
            current_procinfo.flags,
            assigned(current_procinfo.get_first_nestedproc));

        { -OoIPACP: stash this routine's pre-codegen body as a clone template
          for later callers, then scan its own body for calls that pass a
          compile-time constant to an eligible parameter of an already-stashed
          routine and retarget them to specialized clones (compiled below,
          after this routine's generate_code_tree). }
        if (cs_opt_ipacp in current_settings.optimizerswitches) and
           (not isnestedproc) and
           (not(df_generic in pd.defoptions)) then
          begin
            ipacp_pending:=TFPObjectList.create(true);
            ipacp_stash_candidate(pd,
              tcgprocinfo(current_procinfo).code,
              current_procinfo.flags,
              assigned(current_procinfo.get_first_nestedproc));
            ipacp_code:=tcgprocinfo(current_procinfo).code;
            tcgprocinfo(current_procinfo).add_to_symtablestack;
            ipacp_process_calls(pd,ipacp_code,ipacp_pending);
            tcgprocinfo(current_procinfo).remove_from_symtablestack;
            tcgprocinfo(current_procinfo).code:=ipacp_code;
          end;

        { When it's a nested procedure then defer the code generation,
          when back at normal function level then generate the code
          for all deferred nested procedures and the current procedure }
        if not isnestedproc then
          begin
            if not(df_generic in current_procinfo.procdef.defoptions) then
              begin
                { also generate the bodies for all previously done
                  specializations so that we might inline them }
                generate_specialization_procs;
                { convert all load nodes that might have been captured by a
                  capture object }
                tcgprocinfo(current_procinfo).convert_captured_syms;
                tcgprocinfo(current_procinfo).generate_code_tree;
              end;
          end;

        { -OoPARTIALINLINE: the body has now been compiled unchanged; build the
          tiny inline header (it takes over the original procsym) and compile it }
        if pi_dosplit then
          begin
            pi_headerpd:=partialinline_make_header(pd,pi_headercode);
            if assigned(pi_headerpd) then
              compile_partial_inline_header(pi_headerpd,pi_headercode);
          end;

        { -OoIPACP: now that the caller has been code-generated (its call sites
          already retargeted to the clone symbols), compile the specialized
          clone bodies that were synthesised for it. }
        if assigned(ipacp_pending) then
          begin
            for ipacp_i:=0 to ipacp_pending.count-1 do
              compile_ipacp_clone(tipacpclone(ipacp_pending[ipacp_i]).clonepd,
                tipacpclone(ipacp_pending[ipacp_i]).clonecode);
            freeandnil(ipacp_pending);
          end;

        { release procinfo }
        if tprocinfo(current_module.procinfo)<>current_procinfo then
          internalerror(200304274);
        current_module.procinfo:=current_procinfo.parent;

        { For specialization we didn't record the last semicolon. Moving this parsing
          into the parse_body routine is not done because of having better file position
          information available }
        if not current_procinfo.procdef.is_specialization and
            not (po_anonymous in current_procinfo.procdef.procoptions) and
            (
              not assigned(current_procinfo.procdef.struct) or
              not (df_specialization in current_procinfo.procdef.struct.defoptions)
              or not (
                assigned(current_procinfo.procdef.owner) and
                (current_procinfo.procdef.owner.defowner=current_procinfo.procdef.struct)
              )
            ) then
          consume(_SEMICOLON);

        if not isnestedproc then
          { current_procinfo is checked for nil later on }
          freeandnil(current_procinfo);
      end;


    procedure read_proc_body(pd:tprocdef);
      var
        old_module_procinfo : tobject;
        old_current_procinfo : tprocinfo;
      begin
        old_current_procinfo:=current_procinfo;
        old_module_procinfo:=current_module.procinfo;
        current_procinfo:=nil;
        current_module.procinfo:=nil;
        read_proc_body(nil,pd);
        current_procinfo:=old_current_procinfo;
        current_module.procinfo:=old_module_procinfo;
      end;


    function read_async_block:tprocdef;
      var
        pd : tprocdef;
        cancelsym : tparavarsym;
        old_current_procinfo : tprocinfo;
        old_current_structdef : tabstractrecorddef;
      begin
        { build the paramless anonymous procdef the same way parse_proc_dec does
          (current_procinfo reset), then let read_proc parse the body }
        result:=nil;
        old_current_procinfo:=current_procinfo;
        old_current_structdef:=current_structdef;
        current_procinfo:=nil;
        current_structdef:=nil;
        if parse_proc_head(nil,potype_procedure,[ppf_anonymous],nil,nil,pd) then
          begin
            if assigned(pd) then
              begin
                // the spawning future's cooperative-cancel flag, readable in
                // the block as `Cancelled`; the worker thunk passes the impl
                // field by reference. constref makes user writes an error,
                // volatile keeps every check a memory read
                cancelsym:=cparavarsym.create('Cancelled',10,vs_constref,pasbool8type,[]);
                include(cancelsym.varoptions,vo_volatile);
                include(cancelsym.symoptions,sp_internal);
                pd.parast.insertsym(cancelsym);
                parse_proc_dec_finish(pd,[ppf_anonymous],nil);
                { the usefwpd path of read_proc skips the calling-convention
                  setup, so do it here (this also fills pd.paras) }
                handle_calling_convention(pd,hcc_default_actions_impl);
              end;
            current_procinfo:=old_current_procinfo;
            current_structdef:=old_current_structdef;
            if assigned(pd) then
              result:=read_proc([rpf_anonymous],pd);
          end
        else
          begin
            current_procinfo:=old_current_procinfo;
            current_structdef:=old_current_structdef;
          end;
      end;


    function read_proc(flags:tread_proc_flags; usefwpd: tprocdef):tprocdef;
      {
        Parses the procedure directives, then parses the procedure body, then
        generates the code for it
      }

        function convert_flags_to_ppf:tparse_proc_flags;inline;
          begin
            result:=[];
            if rpf_classmethod in flags then
              include(result,ppf_classmethod);
            if rpf_generic in flags then
              include(result,ppf_generic);
            if rpf_anonymous in flags then
              include(result,ppf_anonymous);
          end;

      var
        old_current_procinfo : tprocinfo;
        old_current_structdef: tabstractrecorddef;
        old_current_genericdef,
        old_current_specializedef: tstoreddef;
        pdflags    : tpdflags;
        firstpd : tprocdef;
{$ifdef genericdef_for_nested}
        def : tprocdef;
        srsym : tsym;
        i : longint;
{$endif genericdef_for_nested}
      begin
         { save old state }
         old_current_procinfo:=current_procinfo;
         old_current_structdef:=current_structdef;
         old_current_genericdef:=current_genericdef;
         old_current_specializedef:=current_specializedef;

         { reset current_procinfo.procdef to nil to be sure that nothing is writing
           to another procdef }
         current_procinfo:=nil;
         current_structdef:=nil;
         current_genericdef:=nil;
         current_specializedef:=nil;

         if not assigned(usefwpd) then
           { parse procedure declaration }
           result:=parse_proc_dec(convert_flags_to_ppf,old_current_structdef)
         else
           result:=usefwpd;

         { set the default function options }
         if parse_only then
          begin
            result.forwarddef:=true;
            { set also the interface flag, for better error message when the
              implementation doesn't match this header }
            result.interfacedef:=true;
            include(result.procoptions,po_global);
            pdflags:=[pd_interface];
          end
         else
          begin
            pdflags:=[pd_body];
            if (not current_module.in_interface) then
              include(pdflags,pd_implemen);
            if (not current_module.is_unit) or
               create_smartlink_library then
              include(result.procoptions,po_global);
            result.forwarddef:=false;
          end;

         if not assigned(usefwpd) then
           begin
             { parse the directives that may follow }
             parse_proc_directives(result,pdflags);

             if not (rpf_anonymous in flags) then
               { hint directives, these can be separated by semicolons here,
                 that needs to be handled here with a loop (PFV) }
               while try_consume_hintdirective(result.symoptions,result.deprecatedmsg) do
                Consume(_SEMICOLON);

             { Set calling convention }
             if parse_only then
               handle_calling_convention(result,hcc_default_actions_intf)
             else
               handle_calling_convention(result,hcc_default_actions_impl)
           end;

         { search for forward declarations }
         if not proc_add_definition(result) then
           begin
             { One may not implement a method of a type declared in a different unit }
             if assigned(result.struct) and
                (result.struct.symtable.moduleid<>current_module.moduleid) and
                not result.is_specialization then
              begin
                MessagePos1(result.fileinfo,parser_e_method_for_type_in_other_unit,result.struct.typesymbolprettyname);
              end
             { A method must be forward defined (in the object declaration) }
             else if assigned(result.struct) and
                (not assigned(old_current_structdef)) then
              begin
                MessagePos1(result.fileinfo,parser_e_header_dont_match_any_member,result.fullprocname(false));
                tprocsym(result.procsym).write_parameter_lists(result);
              end
             else
              begin
                { Give a better error if there is a forward def in the interface and only
                  a single implementation }
                firstpd:=tprocdef(tprocsym(result.procsym).ProcdefList[0]);
                if (not result.forwarddef) and
                   (not result.interfacedef) and
                   (tprocsym(result.procsym).ProcdefList.Count>1) and
                   firstpd.forwarddef and
                   firstpd.interfacedef and
                   not(tprocsym(result.procsym).ProcdefList.Count>2) and
                   { don't give an error if it may be an overload }
                   not(m_fpc in current_settings.modeswitches) and
                   (not(po_overload in result.procoptions) or
                    not(po_overload in firstpd.procoptions)) then
                 begin
                   MessagePos1(result.fileinfo,parser_e_header_dont_match_forward,result.fullprocname(false));
                   tprocsym(result.procsym).write_parameter_lists(result);
                 end
                else
                  begin
                    if result.is_generic and not assigned(result.struct) then
                      tprocsym(result.procsym).owner.includeoption(sto_has_generic);
                  end;
              end;
           end;

         { Set mangled name }
         proc_set_mangledname(result);

         { inherit generic flags from parent routine }
         if assigned(old_current_procinfo) and
             (old_current_procinfo.procdef.defoptions*[df_specialization,df_generic]<>[]) then
           begin
             if df_generic in old_current_procinfo.procdef.defoptions then
               include(result.defoptions,df_generic);
             if df_specialization in old_current_procinfo.procdef.defoptions then
               begin
                 include(result.defoptions,df_specialization);
                 { the procdefs encountered here are nested procdefs of which
                   their complete definition also resides inside the current token
                   stream, thus access to their genericdef is not required }
                 {$ifdef genericdef_for_nested}
                 { find the corresponding routine in the generic routine }
                 if not assigned(old_current_procinfo.procdef.genericdef) then
                   internalerror(2016121701);
                 srsym:=tsym(tprocdef(old_current_procinfo.procdef.genericdef).getsymtable(gs_local).find(result.procsym.name));
                 if not assigned(srsym) or (srsym.typ<>procsym) then
                   internalerror(2016121702);
                 { in practice the generic procdef should be at the same index
                   as the index of the current procdef, but as there *might* be
                   differences between the amount of defs generated for the
                   specialization and the generic search for the def using
                   parameter comparison }
                 for i:=0 to tprocsym(srsym).procdeflist.count-1 do
                   begin
                     def:=tprocdef(tprocsym(srsym).procdeflist[i]);
                     if (compare_paras(def.paras,result.paras,cp_none,[cpo_ignorehidden,cpo_openequalisexact,cpo_ignoreuniv])=te_exact) and
                         (compare_defs(def.returndef,result.returndef,nothingn)=te_exact) then
                       begin
                         result.genericdef:=def;
                         break;
                       end;
                   end;
                 if not assigned(result.genericdef) then
                   internalerror(2016121703);
                 {$endif}
               end;
           end;

         { compile procedure when a body is needed }
         if (pd_body in pdflags) then
           begin
             read_proc_body(old_current_procinfo,result);
           end
         else
           begin
             { Handle imports }
             if (po_external in result.procoptions) then
               begin
                 import_external_proc(result);
{$ifdef cpuhighleveltarget}
                 { it's hard to factor this out in a virtual method, because the
                   generic version (the one inside this ifdef) doesn't fit in
                   hlcgobj but in symcreat or here, while the other version
                   doesn't fit in symcreat (since it uses the code generator).
                   Maybe we need another class for this kind of code that could
                   either be symcreat- or hlcgobj-based
                 }
                 if (not result.forwarddef) and
                    (result.hasforward) and
                    (proc_get_importname(result)<>'') then
                   begin
                     { we cannot handle the callee-side of variadic functions (and
                       even if we could, e.g. LLVM cannot call through to something
                       else in that case) }
                     if is_c_variadic(result) then
                       Message1(parser_e_callthrough_varargs,result.fullprocname(false));
                     call_through_new_name(result,proc_get_importname(result));
                     include(result.implprocoptions,pio_thunk);
                   end
                 else
{$endif cpuhighleveltarget}
                   begin
                     create_hlcodegen;
                     hlcg.handle_external_proc(
                       current_asmdata.asmlists[al_procedures],
                       result,
                       proc_get_importname(result));
                     destroy_hlcodegen;
                   end
               end;
           end;

         { always register public functions that are only declared in the
           implementation section as they might be called using an external
           declaration from another unit }
         if (po_global in result.procoptions) and
             not result.interfacedef and
             ([df_generic,df_specialization]*result.defoptions=[]) then
           begin
             result.register_def;
             result.procsym.register_sym;
           end;

         { make sure that references to forward-declared functions are not }
         { treated as references to external symbols, needed for darwin.   }

         { make sure we don't change the binding of real external symbols }
         if (([po_external,po_weakexternal]*result.procoptions)=[]) and (pocall_internproc<>result.proccalloption) then
           current_asmdata.DefineProcAsmSymbol(result,result.mangledname,result.needsglobalasmsym);

         current_structdef:=old_current_structdef;
         current_genericdef:=old_current_genericdef;
         current_specializedef:=old_current_specializedef;
         current_procinfo:=old_current_procinfo;
      end;


    procedure import_external_proc(pd:tprocdef);
      var
        name : string;
      begin
        if not (po_external in pd.procoptions) then
          internalerror(2015121101);

        { Import DLL specified? }
        if assigned(pd.import_dll) then
          begin
            if assigned (pd.import_name) then
              current_module.AddExternalImport(pd.import_dll^,
                pd.import_name^,proc_get_importname(pd),
                pd.import_nr,false,false)
            else
              current_module.AddExternalImport(pd.import_dll^,
                proc_get_importname(pd),proc_get_importname(pd),
                pd.import_nr,false,true);
          end
        else
          begin
            name:=proc_get_importname(pd);
            { add import name to external list for DLL scanning }
            if tf_has_dllscanner in target_info.flags then
              current_module.dllscannerinputlist.Add(name,pd);
            { needed for units that use functions in packages this way }
            current_module.add_extern_asmsym(name,AB_EXTERNAL,AT_FUNCTION);
          end;
      end;

{****************************************************************************
                             DECLARATION PARSING
****************************************************************************}

    { search in symtablestack for not complete classes }
    procedure check_forward_class(p:TObject;arg:pointer);
      begin
        if (tsym(p).typ=typesym) and
           (ttypesym(p).typedef.typ=objectdef) and
           (oo_is_forward in tobjectdef(ttypesym(p).typedef).objectoptions) then
          MessagePos1(tsym(p).fileinfo,sym_e_forward_type_not_resolved,tsym(p).realname);
      end;

{$ifdef DEBUG_NODE_XML}
    procedure XMLInitializeNodeFile(RootName, ModuleName: shortstring);
      var
        T: Text;
      begin
        Assign(T, current_module.ppxfilename);
        {$push} {$I-}
        Rewrite(T);
        if IOResult<>0 then
          begin
            Message1(exec_e_cant_create_archivefile,current_module.ppxfilename);
            current_module.ppxfilefail := True;
            Exit;
          end;
        {$pop}
        { Mark the node dump file as available for writing }
        current_module.ppxfilefail := False;
        WriteLn(T, '<?xml version="1.0" encoding="utf-8"?>');
        WriteLn(T, '<', RootName, ' name="', ModuleName, '">');
        Close(T);

        printnodeindention := printnodespacing;
      end;


    procedure XMLFinalizeNodeFile(RootName: shortstring);
      var
        T: Text;
      begin
        if current_module.ppxfilefail then
          Exit;

        current_module.ppxfilefail := True; { File is now considered closed no matter what happens }
        Assign(T, current_module.ppxfilename);
        {$push} {$I-}
        Append(T);
        if IOResult<>0 then
          begin
            Message1(exec_e_cant_create_archivefile,current_module.ppxfilename);
            Exit;
          end;
        {$pop}
        WriteLn(T, '</', RootName, '>');
        Close(T);
      end;
{$endif DEBUG_NODE_XML}

    procedure read_declarations(islibrary : boolean);
      var
        hadgeneric : boolean;

        procedure handle_unexpected_had_generic;
          begin
            if hadgeneric then
              begin
                Message(parser_e_procedure_or_function_expected);
                hadgeneric:=false;
              end;
          end;

      var
        is_classdef:boolean;
        flags : tread_proc_flags;
      begin
        is_classdef:=false;
        hadgeneric:=false;
        repeat
           if not assigned(current_procinfo) then
             internalerror(200304251);
           case current_scanner.token of
              _LABEL:
                begin
                  handle_unexpected_had_generic;
                  label_dec;
                end;
              _CONST:
                begin
                  handle_unexpected_had_generic;
                  const_dec(hadgeneric);
                end;
              _TYPE:
                begin
                  handle_unexpected_had_generic;
                  type_dec(hadgeneric);
                end;
              _VAR:
                begin
                  handle_unexpected_had_generic;
                  var_dec(hadgeneric);
                end;
              _THREADVAR:
                begin
                  handle_unexpected_had_generic;
                  threadvar_dec(hadgeneric);
                end;
              _CLASS:
                begin
                  is_classdef:=false;
                  if try_to_consume(_CLASS) then
                   begin
                     { class modifier is only allowed for procedures, functions, }
                     { constructors, destructors                                 }
                     if not((current_scanner.token in [_FUNCTION,_PROCEDURE,_OPERATOR]) or (current_scanner.token=_DESTRUCTOR) or (current_scanner.token=_CONSTRUCTOR)) and
                        not((current_scanner.token=_ID) and (current_scanner.idtoken=_OPERATOR)) then
                       Message(parser_e_procedure_or_function_expected);

                     if is_interface(current_structdef) then
                       Message(parser_e_no_static_method_in_interfaces)
                     else
                       { class methods are also allowed for Objective-C protocols }
                       is_classdef:=true;
                   end;
                end;
              _CONSTRUCTOR,
              _DESTRUCTOR,
              _FUNCTION,
              _PROCEDURE,
              _OPERATOR:
                begin
                  if hadgeneric and not (current_scanner.token in [_PROCEDURE,_FUNCTION]) then
                    begin
                      Message(parser_e_procedure_or_function_expected);
                      hadgeneric:=false;
                    end;
                  flags:=[];
                  if is_classdef then
                    include(flags,rpf_classmethod);
                  if hadgeneric then
                    include(flags,rpf_generic);
                  read_proc(flags,nil);
                  is_classdef:=false;
                  hadgeneric:=false;
                end;
              _EXPORTS:
                begin
                   handle_unexpected_had_generic;
                   if (current_procinfo.procdef.localst.symtablelevel>main_program_level) then
                     begin
                        Message(parser_e_syntax_error);
                        consume_all_until(_SEMICOLON);
                     end
                   else if islibrary or
                     (target_info.system in systems_unit_program_exports) then
                     read_exports
                   else
                     begin
                        Message(parser_w_unsupported_feature);
                        consume(_BEGIN);
                     end;
                end;
              _PROPERTY:
                begin
                  handle_unexpected_had_generic;
                  if (m_fpc in current_settings.modeswitches) then
                    property_dec
                  else
                    break;
                end;
              else
                begin
                  case current_scanner.idtoken of
                    _RESOURCESTRING:
                      begin
                        handle_unexpected_had_generic;
                        { m_class is needed, because the resourcestring
                          loading is in the ObjPas unit }
{                        if (m_class in current_settings.modeswitches) then}
                          resourcestring_dec(hadgeneric)
{                        else
                          break;}
                      end;
                    _OPERATOR:
                      begin
                        handle_unexpected_had_generic;
                        if is_classdef then
                          begin
                            read_proc([rpf_classmethod],nil);
                            is_classdef:=false;
                          end
                        else
                          break;
                      end;
                    _GENERIC:
                      begin
                        handle_unexpected_had_generic;
                        if not (m_implicit_generics in current_settings.modeswitches) then
                          begin
                            consume(_ID);
                            hadgeneric:=true;
                          end
                        else
                          break;
                      end;
                    _STATIC:
                      begin
                        handle_unexpected_had_generic;
                        if m_static_section in current_settings.modeswitches then
                          static_dec(hadgeneric)
                        else
                          break;
                      end;
                    _THREADSTATIC:
                      begin
                        handle_unexpected_had_generic;
                        if m_thread_static in current_settings.modeswitches then
                          threadstatic_dec(hadgeneric)
                        else
                          break;
                      end
                    else
                      break;
                  end;
                end;
           end;
         until false;

         { add implementations for synthetic method declarations added by
           the compiler (not for unit/program init functions, their localst
           is the statistic -> would duplicate the work done in pmodules) }
         if (current_procinfo.procdef.localst.symtabletype=localsymtable) and
           { we cannot call add_synthetic_method_implementations as it throws an internalerror if
             the token is a string/char. As this is a syntax error and compilation will abort anyways,
             skipping the call does not matter
           }
           (current_scanner.token<>_CSTRING) and
           (current_scanner.token<>_CWCHAR) and
           (current_scanner.token<>_CWSTRING) then
           add_synthetic_method_implementations(current_procinfo.procdef.localst);

         { check for incomplete class definitions, this is only required
           for fpc modes }
         if (m_fpc in current_settings.modeswitches) then
           current_procinfo.procdef.localst.SymList.ForEachCall(@check_forward_class,nil);
      end;


    procedure read_interface_declarations;
      var
        hadgeneric : boolean;

        procedure handle_unexpected_had_generic;
          begin
            if hadgeneric then
              begin
                Message(parser_e_procedure_or_function_expected);
                hadgeneric:=false;
              end;
          end;

      var
        flags : tread_proc_flags;
      begin
         hadgeneric:=false;
         repeat
           case current_scanner.token of
             _CONST :
               begin
                 handle_unexpected_had_generic;
                 const_dec(hadgeneric);
               end;
             _TYPE :
               begin
                 handle_unexpected_had_generic;
                 type_dec(hadgeneric);
               end;
             _VAR :
               begin
                 handle_unexpected_had_generic;
                 var_dec(hadgeneric);
               end;
             _THREADVAR :
               begin
                 handle_unexpected_had_generic;
                 threadvar_dec(hadgeneric);
               end;
             _FUNCTION,
             _PROCEDURE,
             _OPERATOR :
               begin
                 if hadgeneric and not (current_scanner.token in [_FUNCTION, _PROCEDURE]) then
                   begin
                     message(parser_e_procedure_or_function_expected);
                     hadgeneric:=false;
                   end;
                 flags:=[];
                 if hadgeneric then
                   include(flags,rpf_generic);
                 read_proc(flags,nil);
                 hadgeneric:=false;
               end;
             else
               begin
                 case current_scanner.idtoken of
                   _RESOURCESTRING :
                     begin
                       handle_unexpected_had_generic;
                       resourcestring_dec(hadgeneric);
                     end;
                   _PROPERTY:
                     begin
                       handle_unexpected_had_generic;
                       if (m_fpc in current_settings.modeswitches) then
                         property_dec
                       else
                         break;
                     end;
                   _GENERIC:
                     begin
                       handle_unexpected_had_generic;
                       if not (m_implicit_generics in current_settings.modeswitches) then
                         begin
                           hadgeneric:=true;
                           consume(_ID);
                         end
                       else
                         break;
                     end;
                   _STATIC:
                     begin
                       handle_unexpected_had_generic;
                       if m_static_section in current_settings.modeswitches then
                         static_dec(hadgeneric)
                       else
                         break;
                     end
                   else
                     break;
                 end;
               end;
           end;
         until false;
         { check for incomplete class definitions, this is only required
           for fpc modes }
         if (m_fpc in current_settings.modeswitches) then
          symtablestack.top.SymList.ForEachCall(@check_forward_class,nil);
      end;


end.
